Thankful Pistachio Turkey

Medium

# Lack of Worker registration validation in the `submitDeploymentRequest` leads to protocol disruption and fund lose to users

### Summary

The lack of Worker registration validation in the `submitDeploymentRequest` function will cause a disruption of deployments for users as a malicious actor will claim deployment requests without being a registered Worker, preventing legitimate completion.


### Root Cause

In `BlueprintCore.sol` at the [`submitDeploymentRequest`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L602) function, the function is public and allows any address (except implicitly `address(0)`) to call it and set `requestDeploymentStatus[requestID].deployWorkerAddr` to `msg.sender`, without verifying if the caller is registered in `workersPublicKey` via `setWorkerPublicKey`.

```solidity
function submitDeploymentRequest(bytes32 projectId, bytes32 requestID)
    public
    hasProject(projectId)
    returns (bool isAccepted)
{
    require(requestID.length > 0, "requestID is empty");
    require(requestDeploymentStatus[requestID].status != Status.Init, "requestID does not exist");
    require(
        requestDeploymentStatus[requestID].status != Status.Pickup,
        "requestID already picked by another worker, try a different requestID"
    );
    require(
        requestDeploymentStatus[requestID].status != Status.GeneratedProof, "requestID has already submitted proof"
    );

    requestDeploymentStatus[requestID].status = Status.Pickup;
    requestDeploymentStatus[requestID].deployWorkerAddr = msg.sender;

    isAccepted = true;

    emit AcceptDeployment(projectId, requestID, requestDeploymentStatus[requestID].deployWorkerAddr);
}
```

### Internal Pre-conditions

1. The `submitDeploymentRequest` function remains public and unchanged in the deployed contract.
2. A deployment request exists with `requestDeploymentStatus[requestID].status == Status.Issued`, awaiting Worker pickup.
3. Users have paid for the deployment, expecting a legitimate Worker to complete it.



### External Pre-conditions

1. The Base blockchain allows any address to send transactions to the contract, which is standard behavior for public networks.

### Attack Path

1. A user initiates a deployment request, paying $token and creating a `requestID` with `status = Status.Issued`, linked to a `projectId`.
2. A malicious actor monitors the blockchain and identifies the `requestID` in `Issued` status.
3. The malicious actor calls `submitDeploymentRequest(projectId, requestID)` before a legitimate Worker, setting `requestDeploymentStatus[requestID].status = Status.Pickup` and `deployWorkerAddr = maliciousAddress`.
4. The deployment is locked to the malicious actor, who either fails to submit proof (lacking a valid workersPublicKey entry) or submits invalid proof, preventing completion and locking the `projectId` (`requestDeploymentID != 0` blocks retries).
5. Also, the transfered $token will go to `feeCollectionWalletAddress` and protocol didn't have a functionality to refund this token to the respective users if this issue occurs. So anyway user is gonna lose their fund.



### Impact

- The user suffers an approximate loss of $tokens, due to the failed deployment. The attacker gains no direct funds, incurring only gas costs, but disrupts core functionality by stalling the deployment process.
- Also, the transfered $token will go to feeCollectionWalletAddress and protocol didn't have a functionality to refund this token to the respective users if this issue occurs. So anyway user is gonna lose their fund.


### PoC

_No response_

### Mitigation

Add a check against `workersPublicKey`:
```solidity
function submitDeploymentRequest(bytes32 projectId, bytes32 requestID)
    public
    hasProject(projectId)
    returns (bool isAccepted)
{
    require(workersPublicKey[msg.sender].length > 0, "Caller is not a registered Worker");
    require(requestID.length > 0, "requestID is empty");
    require(requestDeploymentStatus[requestID].status != Status.Init, "requestID does not exist");
    require(
        requestDeploymentStatus[requestID].status != Status.Pickup,
        "requestID already picked by another worker, try a different requestID"
    );
    require(
        requestDeploymentStatus[requestID].status != Status.GeneratedProof, "requestID has already submitted proof"
    );

    requestDeploymentStatus[requestID].status = Status.Pickup;
    requestDeploymentStatus[requestID].deployWorkerAddr = msg.sender;

    isAccepted = true;

    emit AcceptDeployment(projectId, requestID, requestDeploymentStatus[requestID].deployWorkerAddr);
}
```

