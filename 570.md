Huge Lemonade Reindeer

Medium

# Anyone can alter `deploymentProof` for given request id

### Summary

https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L584-L626

The missing guard/check for request id <-> msg.sender in `submitDeploymentRequest` can lead to setting `deployWorkerAddr` to the an arbitrary address which then can be used to submit a malicious/invalid proof with `submitProofOfDeployment`.

### Root Cause

The `submitDeploymentRequest` can be called for existing `requestID` in order to set the deployment's status to `Status.Pickup` and `deployWorkerAddr` to msg.sender from anyone.

```solidity
function submitDeploymentRequest(bytes32 projectId, bytes32 requestID)
    public
    hasProject(projectId)
    returns (bool isAccepted)
{
    require(requestID.length > 0, "requestID is empty");
    require(requestDeploymentStatus[requestID].status != Status.Init, "requestID does not exist");
    require(
        requestDeploymentStatus[requestID].status != Status.Pickup,
        "requestID already picked by another worker, try a different requestID"
    );

    require(
        requestDeploymentStatus[requestID].status != Status.GeneratedProof, "requestID has already submitted proof"
    );

    // currently, do first come, first server, will do a better way in the future
    requestDeploymentStatus[requestID].status = Status.Pickup;
    requestDeploymentStatus[requestID].deployWorkerAddr = msg.sender;

    // set project deployed worker address
    isAccepted = true;

    emit AcceptDeployment(projectId, requestID, requestDeploymentStatus[requestID].deployWorkerAddr);
}
```

After that, the malicious user calls `submitProofOfDeployment` to set a malicious or invalid proof for the same `requestIdD`.

```solidity
function submitProofOfDeployment(bytes32 projectId, bytes32 requestID, string memory proofBase64)
    public
    hasProject(projectId)
{
    require(requestID.length > 0, "requestID is empty");
    require(requestDeploymentStatus[requestID].status != Status.Init, "requestID does not exist");
    require(requestDeploymentStatus[requestID].deployWorkerAddr == msg.sender, "Wrong worker address");
    require(requestDeploymentStatus[requestID].status != Status.GeneratedProof, "Already submitted proof");

    // set deployment status into generatedProof
    requestDeploymentStatus[requestID].status = Status.GeneratedProof;

    // save deployment proof to mapping
    deploymentProof[requestID] = proofBase64;

    emit GeneratedProofOfDeployment(projectId, requestID, proofBase64);
}
```

After the call the status will be set to `Status.GeneratedProof` and the proof will be added/altered.

### Internal Pre-conditions

A random user should create a request in order to malicious user to use an existing request id.

### External Pre-conditions

-

### Attack Path

0. Every project id and request id can be seen by anyone either by monitoring the blockchain or using the getter methods from the contract
1. A random creates a project and submits proof of deployment.
1. A malicious user calls `submitDeploymentRequest` with existing `projectId` and `requestID`. The `deployWorkerAddr` is set to the msg.sender
2. Then, the malicious user calls `submitProofOfDeployment` with same `projectId` and `requestID` and his own crafted malicious/invalid proof.
3. The proof gets successfully altered for the `requestID`.

### Impact

Currently the proof is not used inside the contract. It is only returned by event and getter function but I believe it is a crucial part for offchain validation which can lead to DoS, broken functionality, etc. Depends for what it is used for.

### PoC

_No response_

### Mitigation

Add a mapping which checks if the corresponding project id belongs to msg.sender.