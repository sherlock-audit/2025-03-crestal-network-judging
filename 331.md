Cheesy Tin Dragonfly

High

# Denial of Service via Permanently Locked Deployment Requests  enables malicious actors to block agent deployments

### Summary

The absence of request expiration mechanisms / penalties for workers (worker reputation loss) will cause a denial of service for users and the protocol as malicious workers an permanently block deployment requests by claiming them and never submitting proofs, hence agents are not created.

### Root Cause

The protocol’s deployment request system locks requests irreversibly to workers `(via Status.Pickup)` without Timeouts to auto-release unfulfilled requests or  Penalties (e.g., slashing, reputation loss) for workers who abandon requests

### Internal Pre-conditions

1. Users needs to try create requests by e.g creating an Agent with token
2. The request needs to be first claimed by a malicious worker.


### External Pre-conditions

Nil

### Attack Path

1. User Creates a Deployment Request: A user (e.g., Alice) calls `createAgentWithToken` to initiate a deployment: which eventually calls `CreateAgent`
2. This triggers `deploymentRequest,` which sets the initial status:
3. Worker X Claims the Request
4. Worker X Abandons the Request
5. `requestID` remains in `Status.Pickup` indefinitely
6. No other worker can claim it:
7. Alice (User): Her deployment is permanently stuck.
8. Legitimate Workers: Cannot claim Alice’s request.

### Impact

DOS for users and workers.

### PoC

Step by step process on how this DOS flows
1.  Example, User Creates a Deployment Request, A user (e.g., Alice) calls [`createAgentWithToken`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L479-L489) to initiate a deployment, which makes an internal call to `createAgent`
```solidity
    function createAgentWithToken(
        bytes32 projectId,
        string memory base64Proposal,
        address privateWorkerAddress,
        string memory serverURL,
        address tokenAddress
    ) public returns (bytes32 requestID) {
        require(tokenAddress != address(0), "Token address is empty");


        requestID = createAgent(msg.sender, projectId, base64Proposal, privateWorkerAddress, serverURL, 0, tokenAddress);   // @barz - createAgent is called 
    }
```

2. The [`createAgent`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L427-L478) function triggers [`createCommonProjectIDAndDeploymentRequest`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L359-L389) ---> [`deploymentRequest`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L283-L324), which sets the initial status:
```solidity
 function deploymentRequest(
        address userAddress,
        bytes32 projectId,
        address solverAddress,
        address workerAddress,
        string memory base64Proposal,
        string memory serverURL,
        uint256 index
    ) internal hasProject(projectId) returns (bytes32 requestID, bytes32 projectDeploymentId) {
        require(bytes(serverURL).length > 0, "serverURL is empty");
        require(bytes(base64Proposal).length > 0, "base64Proposal is empty");

        // generate project used deployment id that linked to many deploymentsID associated with different service id
        projectDeploymentId =
            keccak256(abi.encodePacked(block.timestamp, userAddress, base64Proposal, block.chainid, projectId));

        // check projectDeploymentId id is created or not
        // if it is created, which means project has started deployment process, should lock
        // slither-disable-next-line incorrect-equality,timestamp
        require(projects[projectId].requestDeploymentID == 0, "deployment requestID already exists");

        // generate unique deployment requestID message hash
        requestID =
            keccak256(abi.encodePacked(block.timestamp, userAddress, base64Proposal, block.chainid, projectId, index));

        latestDeploymentRequestID[userAddress] = requestID;

        // workerAddress == address(0): init deployment status, not picked by any worker
        // workerAddress != address(0):
        // private deployment request
        // set pick up deployment status since this is private deployment request,
        // which can be picked only by designated worker
        DeploymentStatus memory deploymentStatus = DeploymentStatus({              //@@audit -- here 
            status: (workerAddress == dummyAddress ? Status.Issued : Status.Pickup),
            deployWorkerAddr: workerAddress
        });

        requestDeploymentStatus[requestID] = deploymentStatus;

        // update project solver info
        projects[projectId].proposedSolverAddr = solverAddress;
    }
```
If the `workerAddress` equals the `dummyAddress`, then the ternary operator sets the status to `Status.Issued`. This indicates that the deployment request is unclaimed and available for workers to pick up. However, if `workerAddress` is not equal to `dummyAddress`, the status becomes `Status.Pickup`, implying that a worker has already claimed the request.

The result of it is that, A new deployment request `(requestID)` is created with `Status.Issued` & It is unclaimed and available for workers to process.

3. Now Malicious Worker X Claims the Request by making a call to the [`submitDeploymentRequest`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L602-L626) to claim Alice’s request:
```solidity
function submitDeploymentRequest(bytes32 projectId, bytes32 requestID)
        public
        hasProject(projectId)
        returns (bool isAccepted)
    {
        require(requestID.length > 0, "requestID is empty");
        require(requestDeploymentStatus[requestID].status != Status.Init, "requestID does not exist");
        require(
            requestDeploymentStatus[requestID].status != Status.Pickup,
            "requestID already picked by another worker, try a different requestID"
        );

        require(
            requestDeploymentStatus[requestID].status != Status.GeneratedProof, "requestID has already submitted proof"
        );

        // currently, do first come, first server, will do a better way in the future
        requestDeploymentStatus[requestID].status = Status.Pickup;      // @barz audit Alice’s request (requestID) is now in Status.Pickup.
        requestDeploymentStatus[requestID].deployWorkerAddr = msg.sender;  // // msg.sender = Worker X

        // set project deployed worker address
        isAccepted = true;

        emit AcceptDeployment(projectId, requestID, requestDeploymentStatus[requestID].deployWorkerAddr);
    }
```
Alice’s request (requestID) is now in Status.Pickup. Only Worker X can process it.

4. Worker X Abandons the Request:-- Worker X never calls [`submitProofOfDeployment`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L584-L600)
```solidity
  function submitProofOfDeployment(bytes32 projectId, bytes32 requestID, string memory proofBase64)       /
        public
        hasProject(projectId)
    {
        require(requestID.length > 0, "requestID is empty");
        require(requestDeploymentStatus[requestID].status != Status.Init, "requestID does not exist");
        require(requestDeploymentStatus[requestID].deployWorkerAddr == msg.sender, "Wrong worker address");   //  @barz  audit -- ❌ Worker X never triggers this
        require(requestDeploymentStatus[requestID].status != Status.GeneratedProof, "Already submitted proof");

        // set deployment status into generatedProof
        requestDeploymentStatus[requestID].status = Status.GeneratedProof;

        // save deployment proof to mapping
        deploymentProof[requestID] = proofBase64;

        emit GeneratedProofOfDeployment(projectId, requestID, proofBase64);
    }
```
This results in the `requestID` remaining in `Status.Pickup` indefinitely,  **and No other worker can claim it**:

```solidity
function submitDeploymentRequest(bytes32 projectId, bytes32 requestID)
        public
        hasProject(projectId)
        returns (bool isAccepted)
    {
        require(requestID.length > 0, "requestID is empty");
        require(requestDeploymentStatus[requestID].status != Status.Init, "requestID does not exist");
        require(
            requestDeploymentStatus[requestID].status != Status.Pickup,
            "requestID already picked by another worker, try a different requestID"    // @barz audit -- This check blocks all other workers from taking over the request.
        );

        require(
            requestDeploymentStatus[requestID].status != Status.GeneratedProof, "requestID has already submitted proof"
        );

        // currently, do first come, first server, will do a better way in the future
        requestDeploymentStatus[requestID].status = Status.Pickup;        
        requestDeploymentStatus[requestID].deployWorkerAddr = msg.sender;

        // set project deployed worker address
        isAccepted = true;

        emit AcceptDeployment(projectId, requestID, requestDeploymentStatus[requestID].deployWorkerAddr);
    }
```
This check blocks all other workers from taking over the request.

The impact of this on the protocol is that a. Alice (User): Her deployment is permanently stuck.  b. Legitimate Workers: Cannot claim Alice’s request.


**Link to snippets**
https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L479-L489

https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L427-L478

https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L359-L389

https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L283-L324

https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L602-L626

https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L584-L600

### Mitigation

1. Adding timestamps to DeploymentStatus for auto-release.
2. Implementing reputation penalties or worker slashing.