Dancing Hotpink Tortoise

Medium

# Project Creators will not be able to deploy their projects if created by `createProjectID`

### Summary

The call to `createProjectID()` will cause a deployment DOS for the created project as `setProjectId()` is invoked by every function that initiates a deployment request, preventing any deployment request from being created.

### Root Cause

To create a deployment request users can call one of the following functions:

1. createProjectIDAndDeploymentRequest()
2. createProjectIDAndDeploymentRequestWithSig()
3. createProjectIDAndPrivateDeploymentRequest()

however, all those function call the `createCommonProjectIDAndDeploymentRequest()`:
[BlueprintCore.sol#L359-L367](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L359-L367)
```Solidity
    function createCommonProjectIDAndDeploymentRequest(
        address userAddress,
        bytes32 projectId,
        string memory base64Proposal,
        address workerAddress,
        string memory serverURL
    ) internal returns (bytes32 requestID) {
        // set project id
        setProjectId(projectId, userAddress);

        // create deployment request without solver recommendation, so leave solver address as dummyAddress
        // since this is public deployment request leave worker address as dummyAddress
        bytes32 projectDeploymentId;
        (requestID, projectDeploymentId) =
            deploymentRequest(userAddress, projectId, dummyAddress, workerAddress, base64Proposal, serverURL, 0);
        totalDeploymentRequest++;

        projects[projectId].requestDeploymentID = projectDeploymentId;

        deploymentIdList[projectDeploymentId].push(requestID);

        if (workerAddress == dummyAddress) {
            emit RequestDeployment(projectId, userAddress, dummyAddress, requestID, base64Proposal, serverURL);
        } else {
            emit RequestPrivateDeployment(
                projectId, userAddress, workerAddress, dummyAddress, requestID, base64Proposal, serverURL
            );
            // emit accept deployment event since this deployment request is accepted by blueprint
            emit AcceptDeployment(projectId, requestID, workerAddress);
        }
    }
```
Moreover, as you can see the first thing that this function does is calling the `setProjectId()`. However, all those functions will result in a revert if the project was already created by calling `createProjectID()`, because `setProjectId()` check if the project has already been created before.
```Solidity
    function setProjectId(bytes32 projectId, address userAddr) internal newProject(projectId) {
        require(userAddr != dummyAddress, "Invalid userAddr");

        Project memory project =
            Project({id: projectId, requestProposalID: 0, requestDeploymentID: 0, proposedSolverAddr: dummyAddress});
        // set project info into mapping
        projects[projectId] = project;

        // set latest project
        latestProjectID[userAddr] = projectId;

        emit CreateProjectID(projectId, userAddr);
    }
```



### Internal Pre-conditions

The user need to call `createProjectID()` before trying to create a deployment request for his project.

### External Pre-conditions

N/A

### Attack Path

1. user create a new project by calling `createProjectID()`
2. user try to create a deployment request by calling one of the following functions `createProjectIDAndDeploymentRequest()` or `createProjectIDAndDeploymentRequestWithSig()` or `createProjectIDAndPrivateDeploymentRequest()`

### Impact

Users will not be able to deploy their projects.

### PoC

```Solidity

    function test_createProjectIDAndDeploymentRequest() public {
        bytes32 projectId2 = blueprint.createProjectID();
        bytes32 requestID2 = blueprint.createProjectIDAndDeploymentRequest(projectId2, "base64Proposal","serverURL");
    }
```

this PoC will revert due to "projectId already exists"

### Mitigation

to mitigate this vulnerability, you should avoid resetting a project that is already created.