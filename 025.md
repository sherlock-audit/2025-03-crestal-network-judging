Eager Paisley Mole

Medium

# Users Will Lose Funds Locked In Contract As A Result Of `userTopUp`

### Summary

The absence of a withdrawal or utilization mechanism for topped-up funds will cause a permanent loss of funds for users as the contract locks the topped-up amounts in the `userTopUpMp` mapping without providing any way to retrieve or use them.


### Root Cause

https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L751-L762

The `BlueprintCore.sol::userTopUp` function allows users to transfer ERC20 tokens to the `feeCollectionWalletAddress` and records the topped-up amount in the `userTopUpMp` mapping. However, there is no corresponding function to withdraw these funds or use them for operations like agent creation or updates, effectively locking the funds in the contract's accounting without a purpose.

### Internal Pre-conditions

1. A user needs to call `userTopUp(address tokenAddress, uint256 amount)` to transfer an amount of ERC20 tokens to the `feeCollectionWalletAddress`.

2. The `paymentAddressEnableMp[tokenAddress]` must be set to true.

### External Pre-conditions

None

### Attack Path

This is not an attack path per se but a vulnerability path due to a design flaw:
1. A user calls `userTopUp(tokenAddress, amount)` with a valid `tokenAddress` and a positive `amount`.
2. The `payWithERC20` function transfers amount of ERC20 tokens from the user to `feeCollectionWalletAddress`.
3. The contract updates `userTopUpMp[msg.sender][tokenAddress]` by adding amount to the user's balance for that token.
4. The user has no way to withdraw or use the recorded balance in `userTopUpMp`, as no other function in the contract interacts with this mapping for spending or refunding purposes.

### Impact

The users suffer a permanent loss of the topped-up funds, equivalent to the total value of ERC20 tokens transferred via userTopUp. These funds are effectively locked in the contract's feeCollectionWalletAddress without any mechanism for retrieval or utilization.

### PoC

Described In Attack Path

### Mitigation

To resolve this issue, implement one or both of the following:
1. Withdrawal Function: Add a function to allow users to withdraw their topped-up funds.
```solidity
function withdrawTopUp(address tokenAddress, uint256 amount) public {
    require(amount > 0, "Amount must be greater than 0");
    require(userTopUpMp[msg.sender][tokenAddress] >= amount, "Insufficient topped-up balance");
    userTopUpMp[msg.sender][tokenAddress] -= amount;
    IERC20(tokenAddress).transferFrom(feeCollectionWalletAddress, msg.sender, amount);
    // Note: Ensure feeCollectionWalletAddress has approved this contract to spend tokens
}
```

2. **Utilization in Operations:** Modify functions like `createAgent` and `updateWorkerDeploymentConfig` to use `userTopUpMp` balances instead of direct ERC20 transfers when applicable.

**For agent creation**
```solidity
function createAgent(
    address userAddress,
    bytes32 projectId,
    string memory base64Proposal,
    address privateWorkerAddress,
    string memory serverURL,
    uint256 tokenId,
    address tokenAddress
) internal returns (bytes32 requestID) {
    if (tokenAddress == address(0)) {
        // create agent with nft
        // check NFT token id is already used or not
        require(nftTokenIdMap[tokenId] != Status.Pickup, "NFT token id already used");

        // check NFT ownership
        require(checkNFTOwnership(nftContractAddress, tokenId, userAddress), "NFT token not owned by user");

        requestID = createCommonProjectIDAndDeploymentRequest(
            userAddress, projectId, base64Proposal, privateWorkerAddress, serverURL
        );

        // update NFT token id status
        nftTokenIdMap[tokenId] = Status.Pickup;

        // set deployment owner
        deploymentOwners[requestID] = userAddress;

        // emit create agent event
        emit CreateAgent(projectId, requestID, userAddress, tokenId, 0);
    } else {
        // create agent with token
        // check token address is valid and in paymentOpCostMp
        require(paymentAddressEnableMp[tokenAddress], "Token address is invalid");

        // get cost of create agent operation
        uint256 cost = paymentOpCostMp[tokenAddress][CREATE_AGENT_OP];
        if (cost > 0) {
            uint256 userBalance = userTopUpMp[userAddress][tokenAddress];
            if (userBalance >= cost) {
                // If balance covers the cost, subtract cost from balance
                userTopUpMp[userAddress][tokenAddress] -= cost;
            } else {
                // If cost exceeds balance, reset balance to zero and pay the deficit
                uint256 deficit = cost - userBalance;
                userTopUpMp[userAddress][tokenAddress] = 0;
                payWithERC20(tokenAddress, deficit, userAddress, feeCollectionWalletAddress);
            }
        }

        requestID = createCommonProjectIDAndDeploymentRequest(
            userAddress, projectId, base64Proposal, privateWorkerAddress, serverURL
        );

        // set deployment owner
        deploymentOwners[requestID] = userAddress;

        // emit create agent event
        emit CreateAgent(projectId, requestID, userAddress, tokenId, cost);
    }
}
```


**For Agent Update**
```solidity
function updateWorkerDeploymentConfigCommon(
    address tokenAddress,
    address userAddress,
    bytes32 projectId,
    bytes32 requestID,
    string memory updatedBase64Config
) internal hasProject(projectId) {
    require(requestDeploymentStatus[requestID].status != Status.Init, "requestID does not exist");
    require(bytes(updatedBase64Config).length > 0, "updatedBase64Config is empty");
    require(requestDeploymentStatus[requestID].status != Status.Issued, "requestID is not picked up by any worker");

    // check if it owner of requestID
    require(deploymentOwners[requestID] == userAddress, "Only deployment owner can update config");

    // check tokenAddress is valid and must be in paymentOpCostMp
    require(paymentAddressEnableMp[tokenAddress], "Invalid token address");

    // get update agent cost
    uint256 cost = paymentOpCostMp[tokenAddress][UPDATE_AGENT_OP];

    if (cost > 0) {
        uint256 userBalance = userTopUpMp[userAddress][tokenAddress];
        if (userBalance >= cost) {
            // If balance covers the cost, subtract cost from balance
            userTopUpMp[userAddress][tokenAddress] -= cost;
        } else {
            // If cost exceeds balance, reset balance to zero and pay the deficit
            uint256 deficit = cost - userBalance;
            userTopUpMp[userAddress][tokenAddress] = 0;
            payWithERC20(tokenAddress, deficit, userAddress, feeCollectionWalletAddress);
        }
    }

    // reset status if it is generated proof
    if (requestDeploymentStatus[requestID].status == Status.GeneratedProof) {
        requestDeploymentStatus[requestID].status = Status.Pickup;
    }

    emit UpdateDeploymentConfig(
        projectId, requestID, requestDeploymentStatus[requestID].deployWorkerAddr, updatedBase64Config
    );
}
```